<!DOCTYPE html>
<html lang="en">

  <head>
  <meta charset="utf-8" />
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0">

  <title>Perl 6 中的特性(一)</title>
  
  <meta name="author" content=""/>
  <meta name="generator" content="Hugo 0.18.1" />
  <link href='https://ohmysummer.github.io/img/favicon.ico' rel='icon' type='image/x-icon'/>

  <link rel="alternate" href="https://ohmysummer.github.io/index.xml" type="application/rss+xml" title="Young For Perl 6">

  <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/font-awesome/4.7.0/css/font-awesome.min.css" />
  <link rel="stylesheet" href="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/css/bootstrap.min.css" integrity="sha384-BVYiiSIFeK1dGmJRAkycuHAHRg32OmUcww7on3RYdg4Va+PmSTsz/K68vbdEjh4u" crossorigin="anonymous">
  <link rel="stylesheet" href="https://ohmysummer.github.io/css/main.css" />
  <link rel="stylesheet" href="https://fonts.googleapis.com/css?family=Lora:400,700,400italic,700italic" />
  <link rel="stylesheet" href="https://fonts.googleapis.com/css?family=Open+Sans:300italic,400italic,600italic,700italic,800italic,400,300,600,700,800" />
  <link rel="stylesheet" href="https://ohmysummer.github.io/css/pygment_highlights.css" />
  <link rel="stylesheet" href="https://ohmysummer.github.io/css/highlight.min.css">
  
  
  <meta property="og:title" content="Perl 6 中的特性(一)" />
  <meta property="og:type" content="website" />
  <meta property="og:url" content="/post/Perl6%E4%B8%AD%E7%9A%84%E7%89%B9%E6%80%A7%28%E4%B8%80%29/" />
  <meta property="og:image" content="/img/avatar-icon.jpg" />
  
</head>


  <body>

    <nav class="navbar navbar-default navbar-fixed-top navbar-custom">
  <div class="container-fluid">
    <div class="navbar-header">
      <button type="button" class="navbar-toggle" data-toggle="collapse" data-target="#main-navbar">
        <span class="sr-only">Toggle navigation</span>
        <span class="icon-bar"></span>
        <span class="icon-bar"></span>
        <span class="icon-bar"></span>
      </button>
      <a class="navbar-brand" href="https://ohmysummer.github.io">Young For Perl 6</a>
    </div>

    <div class="collapse navbar-collapse" id="main-navbar">
      <ul class="nav navbar-nav navbar-right">
      
        
          <li>
          <a title="博客" href="/">博客</a>
  	      </li>
  	    
      
        
          <li>
          <a title="归档" href="/categories">归档</a>
  	      </li>
  	    
      
        
          <li>
          <a title="关于" href="/page/about/">关于</a>
  	      </li>
  	    
      
        
          <li>
          <a title="标签" href="/tags">标签</a>
  	      </li>
  	    
      
      </ul>
    </div>

	<div class="avatar-container">
	  <div class="avatar-img-border">
      
          <a title="Young For Perl 6" href="https://ohmysummer.github.io">
              <img class="avatar-img" src="https://ohmysummer.github.io/img/avatar-icon.jpg" alt="Young For Perl 6" />
          </a>
      
	  </div>
	</div>

  </div>
</nav>

    



  

  <header class="header-section ">
  
  <div class="intro-header no-img">
      <div class="container">
    <div class="row">
      <div class="col-lg-8 col-lg-offset-2 col-md-10 col-md-offset-1">
        <div class="post-heading">
          <h1>Perl 6 中的特性(一)</h1>
      
      
      
      <span class="post-meta">焉知非鱼 · 2016/04/22</span>
      
        </div>
      </div>
    </div>
  </div>
  </div>
  </header>


    <div class="container" role="main">	  
      
        <div class="container">
  <div class="row">
    <div class="col-lg-8 col-lg-offset-2 col-md-10 col-md-offset-1">
      <article role="main" class="blog-post">
          

<h2 id="基础">基础</h2>

<hr />

<h3 id="数组">数组</h3>

<hr />

<p>切片, 索引, Zen</p>

<pre><code class="language-perl6">use v6;

my @primes = (2,3,5,7,11,13,17,19,23);   # an array gets filled like in Perl 5
# my @primes =  2,3,5,7,11,13,17,19,23 ; # same thing, since unlike P5 round braces just do group
# my @primes = &lt;2 3 5 7 11 13 17 19 23&gt;; # dito, &lt;&gt; is the new qw()

say @primes[];                           # 2 3 5 7 11 13 17 19 23
my $arrayref = [2,3,5,7,11,13,17,19,23]; # in scalar context you get automatically a reference
say @$arrayref;                          # 2 3 5 7 11 13 17 19 23

my $arrayref = item @primes;             # same thing, more explicit
say $arrayref;

my $arrayref = [13,];                    # comma is the new array generator
say $arrayref;

my @primes = 2;                          # array with one element
my @primes = [2,3,5,7,11,13,17,19,23];   # array with one element (arrayref)
say @primes;                             # 2 3 5 7 11 13 17 19 23

my @dev    = {'dan' =&gt; 'parrot'};        # array with one element (hashref)
say @dev;                                # (&quot;dan&quot; =&gt; &quot;parrot&quot;).hash


my @data   = [1..5],[6..10],[11..15];    # Array of Arrays (AoA)
say @data.perl;                          # Array.new([1, 2, 3, 4, 5], [6, 7, 8, 9, 10], [11, 12, 13, 14, 15])

# my @list   = lol @data;                # no change
# say @list.perl;
my @list   = flat @data;                 # returns 1..15
say @list;                               # 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15
</code></pre>

<!-- more -->

<p>函数式编程:</p>

<pre><code class="language-perl6">my @array = 1,3,5,7,9;

my @arrayplus3 = map( *+3, @array);
say @arrayplus3;

my @arrayplusab = map( *+*+3, @array,11);
say @arrayplusab;
</code></pre>

<p>操作数组中的元素: elems, end, cat, shift, unshift, push, pop, splice, delete, exists, pick, roll&hellip;</p>

<pre><code class="language-perl6">﻿use v6;
my @rray=17,21,34,47,58,69;

say 'the array has element' if  ? @rray;   # boolean context, Bool::True if array has any value in it, even if its a 0
say 'the array has ',+ @rray ~' element';  # numeric context, number of elements (like in Perl 5 scalar @a)
say ~ @rray;              # string context, you get content of all cells, stringified and joined, same as &quot;@primes[]&quot;

say @rray.elems;          # same as + @rray
say @rray.end;            # number of the last element, equal to @rray.elems-1, # 5
# say @rray.cat;          # same ~ @rray
say @rray.join('-');      # also same result, you can put another string as parameter that gets between all values
say @rray.unshift(7);     # prepend one value to the array, # 7 1 2 3 4 5 6
say @rray.shift;          # remove the first value and return it # 7
say @rray.push(8);        # add one value on the end # 1 2 3 4 5 6 8
say @rray.pop;            # remove one value from the end and return it # 8

my $n=2;
my $pos=1;
my @ind=0..3;
say @rray.splice($pos,$n);# remove on $pos $n values and replace them with values that follow that two parameter
say @rray:delete(@ind);   # delete all cell with indecies of @ind # 1 4 5 6
say @rray:exists(@ind);   # Bool::True if all indecies of @ind have a value (can be 0 or '')

say '-' x 18;
say @rray;
say @rray.pick([$n]);     # return $n (default is 1) randomly selected values, without duplication
say @rray.roll([$n]);     # return $n (default is 1) randomly selected values, duplication possible (就像掷筛子)
say @rray.reverse;        # all elements in reversed order
say @rray.rotate(-$n);    # returns a list where $n times first item is taken to last position if $n is positive, if negative the other way around
# @rray.sort($coderef); # returns a sorted list by a userdefined criteria, default is alphanumerical sorting
say @rray.min;            # numerical smallest value of that array
say @rray.max;            # numerical largest value of that array
my ($a,$b)= @rray.minmax;  # both at once, like in .sort . min or .max a sorting algorith can be provided
say $a,&quot;   $b&quot;;
# @rray.map($coderef);  # high oder map function, runs $coderef with every value as $_ and returns the list or results
# @rray.classify($cr);  # kind of map, but creates a hash, where keys are the results of $cr and values are from @rray
# @rray.categorize($cr);# kind of classify, but closure can have no (Nil) or several results, so a key can have a list of values
say @rray.grep({$_&gt;40});   # high order grep, returns only these elements that pass a condition ($cr returns something positive)
# @rray.first($coder);  # kind of grep, return just the first matching value
# say @rray.zip;            # join arrays by picking first element left successively from here and then there
</code></pre>

<p>数组的索引:</p>

<pre><code class="language-perl6">use v6;
my  @primes=&lt;1 3 5 7 9 11 13 15 17&gt;;
say @primes;                       # all values as list
say @primes.values;                # same thing
say @primes.keys;                  # list of all indices
say &quot;@primes[]&quot;;                   # insert all values in a string, uses [] as distinction from mail adresses
my  $prime = @primes[0];           # get the first prime
say $prime;

my $last_prime = @primes[*-1];    # get the last one
say $last_prime;

my @some = @primes[2..5];         # get several
say @some;

my @data   = [1..5],[6..10],[11..15];  # Array of Arrays (AoA)
my $cell = @data[1][2];                # get 8, third value of second value (list)
say $cell;

my  $same_cell = @data[1,2];           # same thing, shorten syntax
say $same_cell;                        # 6 7 8 9 10 11 12 13 14 15

my @numbers = @data[1];                # get a copy of the second subarray (6..10)
say @numbers;
my @copy = @data;                      # copy the whole AoA, no more reference passing, use binding instead
say @copy.perl;

</code></pre>

<p>二维数组:</p>

<pre><code class="language-perl6">use v6;
my @original = [1,2],[3,4];
say @original.perl;
my $copy = @original[0]; # $copy points to [1,2]
say $copy;
@original[0][0] = 'fresh stuff'; # $copy[0] holds still 1
say @original;
say $copy;
</code></pre>

<p>根据数组中元素的字符数进行归类:</p>

<pre><code class="language-perl6">my @names = &lt;Patrick Jonathan Larry Moritz Audrey&gt;;
say .key, &quot;\t&quot;, ~.values
    for @names.classify( *.chars );
# 根据字符串的字符个数分类
# Output:
# 7 Patrick
# 8 Jonathan
# 5 Larry
# 6 Moritz Audrey

</code></pre>

<p>根据字符串中单词数进行归类并按单词个数多少进行排序</p>

<pre><code class="language-perl6">#.say for slurp(&quot;README.txt&quot;)\           # whole file into string
#         .words()\                      # split into list of words
#         .classify( *.Str );

my @a = slurp(&quot;README.txt&quot;).words;
# .say  for @a.classify( *.Str );
# output:
#  可见输出的是一个散列
# that =&gt; that that that that that that that that that that that
# the =&gt; the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the the
# is =&gt; is is is is is is is is is is is is is is is is
my %hash = @a.classify( *.Str );
for %hash.sort({-.value.elems}).hash.kv.[^20] -&gt; $key, $value {
    say $key ,&quot;\t&quot;, $value.elems;

}
</code></pre>

<p><code>\</code> 的作用是续行符, 隐藏它后面的空白符:</p>

<pre><code class="language-perl6">.say for slurp(&quot;README.txt&quot;)\           # whole file into string
         .words()\                      # split into list of words
         .classify( *.Str )\            # group words with multiplicity
         .map({;.key =&gt; .value.elems })\
                                        # turn lists into lengths
         .sort( { -.value } )\          # sort descending
         .[ ^10 ];                      # 10 most common words

# Output:
# the =&gt; 40
# to =&gt; 21
# is =&gt; 16
# a =&gt; 16
# that =&gt; 11
# be =&gt; 11
# stack =&gt; 10
# implementation =&gt; 8
# link =&gt; 8
# it =&gt; 7

</code></pre>

<pre><code class="language-perl6">use v6;
my $range = 'a' .. 'f';
for $range.list -&gt; $elem {
    say $elem;
}

.say for @( $range );
# .say for $list.list;
.say for $range.flat;

</code></pre>

<pre><code class="language-perl6">use v6;
my @a=&lt;aa bb cc dd ee ff &gt;;
for @a -&gt; $x,$y,$z {
say $x,$y,$z;
}
say $*PROGRAM_NAME;

aabbcc
ddeeff
three.p6
</code></pre>

<h3 id="hash">Hash</h3>

<hr />

<pre><code class="language-perl6">my $a = bag &lt;a a a b b c&gt;;
my $b = bag &lt;a b b b&gt;;

say $a (|) $b;
say $a (&amp;) $b;
say $a (+) $b;
say $a (.) $b;

</code></pre>

<pre><code class="language-perl6">use v6;
# my %words := KeyBag.new;
# for slurp.comb(/\w+/).map(*.lc) -&gt; $word {
    # %words{$word}++;
# }
my %words := Bag.new(slurp.comb(/\w+/).map(*.lc));

say &quot;%words{}&quot;;
</code></pre>

<pre><code class="language-perl6">my %words;
for slurp.comb(/\w+/).map(*.lc) -&gt; $word {
    %words{$word}++;
}

for %words.pairs.sort(-*.value).map({ $_.key, $_.value }) -&gt; $word, $count {
    say &quot;$word: $count&quot;;
}

</code></pre>

<pre><code class="language-perl6">my %words := Bag.new(slurp.comb(/\w+/).map(*.lc));
# say %words.pairs.list.join(&quot;\n&quot;);
# .say for %words.pairs.sort(-*.value);  # 按键值从大到小排序，然后打印，- 号是降序
# &quot;and&quot; =&gt; 12
# &quot;the&quot; =&gt; 11
# &quot;our&quot; =&gt; 6
# &quot;be&quot; =&gt; 6
# &quot;man&quot; =&gt; 6
# &quot;your&quot; =&gt; 6
# &quot;us&quot; =&gt; 5
# &quot;you&quot; =&gt; 5
# &quot;for&quot; =&gt; 5
# &quot;o&quot; =&gt; 5
# &quot;to&quot; =&gt; 5

for %words.pairs.sort(-*.value).map({ $_.key, $_.value }) -&gt; $word, $count {
    say &quot;$word: $count&quot;;
}
# .say for %words.pairs.sort(-*.value).fmt(&quot;%-15s=&gt;%4d\n&quot;);

</code></pre>

<pre><code class="language-perl6">my %words := bag slurp.comb(/\w+/).map(*.lc);

for %words.pairs.sort(-*.value).map({ $_.key, $_.value }) -&gt; $word, $count {
    say &quot;$word: $count&quot;;
}
</code></pre>

<pre><code class="language-perl6">﻿use v6;

my %dev =  'pugs'=&gt;'audreyt', 'pct'=&gt;'pm', &quot;STD&quot;=&gt;'larry';
my %same_dev = :rakudo('jnthn'), :testsuite('moritz');            # adverb (pair) syntax works as well
my %too_dev = ('audreyt', 'pugs', 'pm', 'pct', 'larry', &quot;STD&quot;);   # lists get autoconverted in hash context
my %compiler = Parrot =&gt; {Rakudo =&gt; 'jnthn'}, SMOP =&gt; {Mildew =&gt; 'ruoso'};       # hash of hashes (HoH)
say %dev.perl;
say %same_dev.perl;
say %too_dev.perl;
say %compiler.perl;

# Hash Slices
my $name='pugs';
my $value = %dev{'pugs'};      # just give me the value related to that key, like in P5
my $value1 = %dev&lt;STD&gt;;         # &lt;&gt; autoquotes like qw() in P5
my $value2 = %dev&lt;&lt;$name&gt;&gt;;     # same thing, just with eval
say $value;
say $value2;

my @values = %dev{'pugs', 'STD'};
my @values2 = %dev&lt;pugs STD&gt;;
my @values3 = %dev&lt;&lt;pugs STD $name&gt;&gt;;
say @values;
say @values2;
say @values3;

say %compiler&lt;Parrot&gt;&lt;Rakudo&gt;; # value in a HoH, returns 'jnthn'
say %compiler&lt;SMOP&gt;;           # returns the Pair: Mildew =&gt; 'ruoso'

# %dev   {'audrey'};           # error, spaces between varname and braces (postcircumfix operator) are no longer allowed
say %dev\  {'pugs'};           # works, quote the space
# %dev   .&lt;dukeleto&gt;;          # error
say %dev\ .{'pugs'};           # works too, &quot;long dot style&quot;, because its its an object in truth
say %dev.{'pugs'};


# Hash Methods
say 'this hash has some pairs' if ? %dev;                    # bool context, true if hash has any pairs
say 'this hash has '~ + %dev ~' pairs';                      # numeric context, returns number of pairs(keys)
say ~ %dev;                     # string context, nicely formatted 2 column table using \t and \n

my $table = %dev;               # same as ~ %dev
say $table;                     # (&quot;pugs&quot; =&gt; &quot;audreyt&quot;, &quot;pct&quot; =&gt; &quot;pm&quot;, &quot;STD&quot; =&gt; &quot;larry&quot;).hash
say %dev.say;                   # stringified, but only $key and $value are separated by \t  #(&quot;pugs&quot; =&gt; &quot;audreyt&quot;, &quot;pct&quot; =&gt; &quot;pm&quot;, &quot;STD&quot; =&gt; &quot;larry&quot;).hash
my  @pairs = %dev;              # list of all containing pairs
say @pairs;                     # &quot;pugs&quot; =&gt; &quot;audreyt&quot; &quot;pct&quot; =&gt; &quot;pm&quot; &quot;STD&quot; =&gt; &quot;larry&quot;
say %dev.pairs;                 # same thing in all context  # &quot;pugs&quot; =&gt; &quot;audreyt&quot; &quot;pct&quot; =&gt; &quot;pm&quot; &quot;STD&quot; =&gt; &quot;larry&quot;
say %dev.elems;                 # same as + %dev or + %dev.pairs  # 3
say %dev.keys;                  # returns the list with all keys
say %dev.values;                # list of all values
say %dev.kv;                    # flat list with key1, value1, key 2 ...
say %dev.invert;                # reverse all key =&gt; value relations
say %dev.push(@pairs);          # inserts a list of pairs, if a key is already present in %dev, both values gets added to an array
# (&quot;pugs&quot; =&gt; [&quot;audreyt&quot;, &quot;audreyt&quot;], &quot;pct&quot; =&gt; [&quot;pm&quot;, &quot;pm&quot;], &quot;STD&quot; =&gt; [&quot;larry&quot;, &quot;larry&quot;]).hash

my @another_pairs='Rakudo'=&gt;'Perl6';
say %same_dev.push(@another_pairs);
</code></pre>

<pre><code class="language-perl6"># You can also destructure hashes (and classes, which you'll learn about later !)
# The syntax is basically `%hash-name (:key($variable-to-store-value-in))`.
# The hash can stay anonymous if you only need the values you extracted.
sub key-of(% (:value($val), :qua($qua))) {
  say &quot;Got val $val, $qua times.&quot;;
}

# Then call it with a hash: (you need to keep the brackets for it to be a hash)
key-of({value =&gt; 'foo', qua =&gt; 1});
my %hash = value =&gt; 'Perl6', qua =&gt; '2016';
key-of(%hash); # the same (for an equivalent `%hash`);

</code></pre>

<pre><code class="language-perl6">my $bag = bag &quot;red&quot; =&gt; 2, &quot;blue&quot; =&gt; 10;
say $bag.roll(10); # 随机生成 10 组
say $bag.pick(*).join(&quot;\n&quot;);

$bag = bag &quot;red&quot; =&gt; 20000000000000000001, &quot;blue&quot; =&gt; 100000000000000000000;
say $bag.roll(10);
say $bag.pick(10).join(&quot; &quot;);
</code></pre>

<h3 id="bind">Bind</h3>

<hr />

<pre><code class="language-perl6">﻿use v6;

my @a := 1..Inf;
say @a[^10]; # 1 2 3 4 5 6 7 8 9 10

my @primes  := @a.grep(*.is-prime);
say @primes[^10]; # 2 3 5 7 11 13 17 19 23 29

my @nprimes := @primes.map({ &quot;{++state $n}: $_&quot; });
.say for @nprimes[^10];

.say for (1..Inf
    ==&gt; grep(*.is-prime)
    ==&gt; map({ &quot;{++state $n}: $_&quot; })
    )[^10];


</code></pre>

<pre><code class="language-perl6">my @a := (1, 2, 3);
say @a.WHAT;
say @a.elems;

my @b = (1, 2, 3);
@b[0] := my $x;
$x = 42;
say @b;

</code></pre>

<pre><code class="language-perl6">use v6;

my $original = 5;say $original;
my $mirror;
$original := $mirror;       # normal binding, done on runtime

say $mirror;

$original ::= $mirror;      # same thing, but done during compile time
say $mirror;
$original = 3;
say $mirror;                # prints 3
$original =:= $mirror;      # true, because their bound together
$original === $mirror;      # alsotrue, because content and type are equal

</code></pre>

<h2 id="io">IO</h2>

<hr />

<pre><code class="language-perl6">my $total = 0;
for @files -&gt; $filename {
    $total += lines($filename.IO).grep(
        { $_ !~~ /&lt;&amp;insigline&gt;/ }
).elems;
 CATCH {
        when X::IO {
            note &quot;Couldn't read $filename&quot;;
} }
}
say $total;
# As CATCH goes inside of the scope,we can see the scope's lexicals!


</code></pre>

<pre><code class="language-perl6">use v6;

# create a file
my $f = open &quot;foo&quot;,:w;
$f.print(time);
$f.close;

# copy
my $io = IO::Path.new(&quot;foo&quot;);
$io.copy(&quot;foo2&quot;);

# clean up
unlink (&quot;foo2&quot;);

# move
$io.rename(&quot;foo2&quot;);

# clean up
unlink (&quot;foo2&quot;);


</code></pre>

<pre><code class="language-perl6">use v6;

# create a file
my $f = open &quot;foo&quot;,:w;
$f.print(time);
$f.close;

unlink &quot;foo&quot;;

</code></pre>

<pre><code class="language-perl6">1428936330
</code></pre>

<pre><code class="language-perl6">1428936330
</code></pre>

<pre><code class="language-perl6">use v6;

my $fn =  $?FILE;

my Instant $i = $fn.IO.accessed;
my $dt = $i.to-posix;

say :$dt.perl;

</code></pre>

<pre><code class="language-perl6"># slurp 读入到数组后只是一个元素
my @lines = slurp('3col.txt');
say @lines.elems;

my $fh = open('3col.txt');
for $fh.lines -&gt; $line {
    say $line;
}


</code></pre>

<pre><code class="language-perl6">﻿&gt; 15 + 27
42
&gt; &lt;beer vodka whisky&gt;.pick
beer
&gt; &lt;beer vodka whisky&gt;.pick(2)
beer whisky
&gt; (1, 1, *+* ... *)[20]
10946
&gt;  dir&gt;&gt;.path
&gt; dir&gt;&gt;.path ==&gt; grep /\.p6$/
REPL.p6 slides.p6

&gt; type REPL.p6 | perl6 -e &quot;$*IN.slurp-rest.comb(/\w+/) ==&gt; sort *.chars ==&gt; @temp ==&gt; reverse @temp ==&gt; @reverse ==&gt; say @reverse
&gt; type REPL.p6 | perl6 -e &quot;$*IN.slurp-rest.comb(/\w+/) ==&gt; sort *.chars ==&gt; reverse @() ==&gt; join \&quot;\n\&quot; ==&gt; say @()&quot;
&gt; type Hamlet.txt | perl6 -e &quot;say [max] $*IN.slurp-rest.comb(/\d+/)&quot;

# slurp reads a file into a scalar
&gt; dir&gt;&gt;.path ==&gt; grep /\.p6$/ ==&gt; sort { slurp($_).chars }
slurp_feed.p6 REPL.p6 slides.p6

# lines reads the lines of a file into an array
&gt; dir&gt;&gt;.path ==&gt; grep /\.p6$/ ==&gt; sort { +lines($_) }
REPL.p6 slides.p6 slurp_feed.p6
# 求出所有 words的和
&gt; type 3col.txt | perl6 -e &quot;say [+] $*IN.lines&gt;&gt;.words&quot;
&gt; type 3col.txt | perl6 -e &quot;say [+] $*IN.lines&gt;&gt;.words&gt;&gt;.elems # word 的个数
&gt; type 3col.txt | perl6 -e &quot;say [+] $*IN.lines&gt;&gt;.words&gt;&gt;.[2]
</code></pre>

<pre><code class="language-perl6">use v6;
my %words;
for slurp.comb(/\w+/).map(*.lc) -&gt; $word {
    %words{$word}++;
}
say %words:kv;
</code></pre>

<h2 id="classes">Classes</h2>

<hr />

<p>Classes In Perl 6</p>

<p>使用带有 block 的 class 关键字引入一个类：</p>

<pre><code class="language-perl6">class Puppy {
    ...
}
</code></pre>

<p>或使用</p>

<pre><code class="language-perl6">class Puppy;
...
1;
</code></pre>

<p>把类相关的东西<code>单独</code>写进一个文件.</p>

<pre><code class="language-perl6">class Enemy {
    method attack-with-arrows   { say &quot;peow peow peow&quot; }
    method attack-with-swords   { say &quot;swish cling clang&quot; }
    method attack-with-fireball { say &quot;sssSSS fwoooof&quot; }
    method attack-with-camelia  { say &quot;flap flap RAWWR!&quot; }
}

# 创建一个方法筛选器， 方法名以 attack-with- 开头
# 对象的 ^methods 方法返回该对象所有的方法，包括自定义的方法
my $selector = { .name ~~ /^ 'attack-with-' / };
given Enemy.new -&gt; $e {
    my $attack-strategy
        = $e.^methods().grep($selector).pick();

    $e.$attack-strategy();           # call a random method
}

</code></pre>

<pre><code class="language-perl6">class Parent {
    method frob {
        say &quot;the parent class frobs&quot;
    }
}

class Child is Parent {
    method frob {
        say &quot;the child's somewhat more fancy frob is called&quot;
    }
}
# 对象的实际类型决定了要调用哪个方法
my Parent $test;
$test = Child.new;
$test.frob;          # calls the frob method of Child rather than Parent

</code></pre>

<pre><code class="language-perl6">class Dog {
    has $.name is rw;
    has $.color;

    method kugo {
       say &quot;hello &quot;,$.name;
    }
}
my $pet = Dog.new(
    name =&gt; 'Spot', color =&gt; 'Black'
);
$pet.kugo();
$pet.name = 'Fido'; # OK
$pet.kugo();
$pet.color = 'White'; # Fails

</code></pre>

<pre><code class="language-perl6">class Journey {
    has $.origin;
    has $.destination;
    has @!travellers;
    has $.notes is rw;

    method add_traveller($name) {
        if $name ne any(@!travellers) {
            push @!travellers, $name;
        } else {
            warn &quot;$name is already going on the journey!&quot;;
        }
    }

    method describe() {
        &quot;From $!origin to $!destination&quot;;
    }
    # Private method
    method !do-something-private($x) {
       ($x + 120)*0.88; # 先加价，再打折！
    }

    method price($x) {
        self!do-something-private(2*$x);
    }

}

my $vacation = Journey.new(
    origin      =&gt; 'China',
    destination =&gt; 'Sweden',
    notes       =&gt; 'Pack hiking'
);

say $vacation.origin;
$vacation.notes = 'Pack hiking gear and sunglasses!';
say $vacation.notes;
$vacation.add_traveller('Larry Wall');
say $vacation.price(40);
$vacation.add_traveller('Larry Wall');

</code></pre>

<pre><code class="language-perl6">use MethodModifiers;
class C {
method m() is before { say &quot;before&quot;; } method m() { say &quot;in the method&quot;; } method m() is after { say &quot;after&quot;; }
} C.m;

</code></pre>

<pre><code class="language-perl6">class Journey {
    has $.origin;
    has $.destination;
    has @!travellers;
    has $.notes is rw;

    method add_traveller($name) {
        if $name ne any(@!travellers) {
            push @!travellers, $name;
        }
        else {
            warn &quot;$name is already going on the journey!&quot;;
        }
    }

    method describe() {
        &quot;From $!origin to $!destination&quot;
    }
}



</code></pre>

<pre><code class="language-perl6">class Point {
    has $.x;
    has $.y = 2 * $!x;
}

my $p = Point.new( x =&gt; 1, y =&gt; 2);
say &quot;x: &quot;, $p.x;
say &quot;y: &quot;, $p.y;

my $p2 = Point.new( x =&gt; 5 );
# the given value for x is used to calculate the right
# value for y.
say &quot;x: &quot;, $p2.x;
say &quot;y: &quot;, $p2.y;

</code></pre>

<pre><code class="language-perl6">class Journey {
    has $.origin;
    has $.destination;
    has @!travellers;
    has $.notes;  # 没有添加 is rw 限制时, 属性默认是只读的!
}

my $j = Journey.new(
    origin      =&gt; 'Sweden',
    destination =&gt; 'China',
    notes       =&gt; 'Be careful your money!'
);

say $j.origin;
say $j.destination;
say $j.notes;

# now, try to change notes
$j.notes = 'gun nima dan'; # Cannot modify an immutable Str
say $j.notes;

</code></pre>

<pre><code class="language-perl6">class Journey {
    has $.origin;
    has $.destination;
    has @!travellers;
    has $.notes is rw;
}

# Create a new instance of the class.
my $vacation = Journey.new(
    origin      =&gt; 'Sweden',
    destination =&gt; 'Switzerland',
    notes       =&gt; 'Pack hiking gear!'
);

# 使用取值器, 这输出 Sweden.
say $vacation.origin;
# 使用 rw 存取器修改属性的值
$vacation.notes = 'Pack hiking gear and sunglasses!';
say $vacation.notes;

</code></pre>

<pre><code class="language-perl6">class Paper   { }
class Scissor { }
class Stone   { }

multi win(Paper   $a, Stone   $b) { 1 }
multi win(Scissor $a, Paper   $b) { 1 }
multi win(Stone   $a, Scissor $b) { 1 }
multi win(Any     $a, Any     $b) { 0 }

say win(Paper.new, Scissor.new); # 0
say win(Stone.new, Stone.new); #0
say win(Paper.new, Stone.new); #1

</code></pre>

<pre><code class="language-perl6">class Point2D {
    has $.x;
    has $.y;

    submethod BUILD(:$!x, :$!y) {
        say &quot;Initalizing Point2D&quot;;
    }
}

class InvertiblePoint2D is Point2D {
    submethod BUILD() {
        say &quot;Initilizing InvertiblePoint2D&quot;;
    }
    method invert {
        self.new(x =&gt; - $.x, y =&gt; - $.y);
    }
}

say InvertiblePoint2D.new( x =&gt; 1, y =&gt; 2);


</code></pre>

<h2 id="junctions">Junctions</h2>

<hr />

<pre><code class="language-perl6">my @bad_ext = ('plx', 'pm', 'pl', 'p6');
my $file_ext = 'p6';
if lc($file_ext) eq any(@bad_ext) {
    say &quot;$file_ext files is  allowed, You are a Perler&quot;;
}


</code></pre>

<pre><code class="language-perl6">use v6;

my $filename = &quot;temp.txt&quot;;
my $fh = open $filename, :w;

for dir(test =&gt; all(/p6$/, /^&lt;-[._]&gt;/ )) -&gt; $file {
    $fh.say(“```perl66”);
    my $string = slurp $file;
    $fh.say($string);
    $fh.say(“```”);
    $fh.say();
}

$fh.close;

</code></pre>

<h2 id="lazy-list">Lazy List</h2>

<hr />

<pre><code class="language-perl6">use v6;
my @integers = 1..*;
    for @integers -&gt; $i {
        say $i;
        last if $i % 17 == 0;
    }
</code></pre>

<pre><code class="language-perl6"># A list comprehension is a special syntax in some programming languages to describe lists. It is similar to the way mathematicians describe sets, with a set comprehension, hence the name.

# Some attributes of a list comprehension are that:
# 1. They should be distinct from (nested) for loops within the syntax of the language.
# 2. They should return either a list or an iterator (something that returns successive members of a collection, in order).
# 3. The syntax has parts corresponding to that of set-builder notation.

# Write a list comprehension that builds the list of all Pythagorean triples with elements between 1 and n. If the language has multiple ways for expressing such a construct (for example, direct list comprehensions and generators), write one example for each.

use v6;

my $n = 20;
my @list := gather for 1..$n -&gt; $x {
         for $x..$n -&gt; $y {
           for $y..$n -&gt; $z {
             take $x,$y,$z if $x*$x + $y*$y == $z*$z;
           }
         }
       }
.say for  @list;

# Note that gather/take is the primitive in Perl 6 corresponding to generators or coroutines in other languages. It is not, however, tied to function call syntax in Perl 6. We can get away with that because lists are lazy, and the demand for more of the list is implicit; it does not need to be driven by function calls.
</code></pre>

<h2 id="loops">Loops</h2>

<hr />

<pre><code class="language-perl6">my $row-count = 6;

constant $peg = &quot;*&quot;;
constant @coin-icons = &quot;\c[UPPER HALF BLOCK]&quot;, &quot;\c[LOWER HALF BLOCK]&quot;;

sub display-board(@positions, @stats is copy, $halfstep) {
    my $coin = @coin-icons[$halfstep.Int];

    state @board-tmpl = {
        # precompute a board
        my @tmpl;
        sub out(*@stuff) {
            @tmpl.push: @stuff&gt;&gt;.ords.item;
        }
        # three lines of space above
        for (1..3) {
            out &quot;  &quot;, &quot; &quot; x (2 * $row-count);
        }
        # $row-count lines of pegs
        for ($row-count...1) Z (1...$row-count) -&gt; $spaces, $pegs {
            out &quot;  &quot;, &quot; &quot; x $spaces, ($peg xx $pegs).join(&quot; &quot;), &quot; &quot; x $spaces;
        }
        # four lines of space below
        for (1..4) {
            out &quot;  &quot;, &quot; &quot; x (2 * $row-count);
        }
        @tmpl
    }();

    my $midpos = $row-count + 2;

    my @output;
    {
        # collect all the output and output it all at once at the end
        sub say(Str $foo) {
            @output.push: $foo, &quot;\n&quot;;
        }
        sub print(Str $foo) {
            @output.push: $foo;
        }

        # make some space above the picture
        say &quot;&quot; for ^10;

        my @output-lines = map { [map *.clone, @$_].item }, @board-tmpl;
        # place the coins
        for @positions.kv -&gt; $line, $pos {
            next unless $pos.defined;
            @output-lines[$line][$pos + $midpos] = $coin.ord;
        }
        # output the board with its coins
        for @output-lines -&gt; @line {
            say @line&gt;&gt;.chr.join(&quot;&quot;);
        }

        # show the statistics
        my $padding = 0;
        while any(@stats) &gt; 0 {
            $padding++;
            print &quot;  &quot;;
            @stats = do for @stats -&gt; $stat {
                given $stat {
                    when 1 {
                        print &quot;\c[UPPER HALF BLOCK]&quot;;
                        $stat - 1;
                    }
                    when * &lt;= 0 {
                        print &quot; &quot;;
                        0
                    }
                    default {
                        print &quot;\c[FULL BLOCK]&quot;;
                        $stat - 2;
                    }
                }
            }
            say &quot;&quot;;
        }
        say &quot;&quot; for $padding...^10;
    }
    say @output.join(&quot;&quot;);
}

sub simulate($coins is copy) {
    my $alive = True;

    sub hits-peg($x, $y) {
        if 3 &lt;= $y &lt; 3 + $row-count and -($y - 2) &lt;= $x &lt;= $y - 2 {
            return not ($x - $y) %% 2;
        }
        return False;
    }

    my @coins = Int xx (3 + $row-count + 4);
    my @stats = 0 xx ($row-count * 2);
    # this line will dispense coins until turned off.
    @coins[0] = 0;
    while $alive {
        $alive = False;
        # if a coin falls through the bottom, count it
        given @coins[*-1] {
            when *.defined {
                @stats[$_ + $row-count]++;
            }
        }

        # move every coin down one row
        for ( 3 + $row-count + 3 )...1 -&gt; $line {
            my $coinpos = @coins[$line - 1];

            @coins[$line] = do if not $coinpos.defined {
                Nil
            } elsif hits-peg($coinpos, $line) {
                # when a coin from above hits a peg, it will bounce to either side.
                $alive = True;
                ($coinpos - 1, $coinpos + 1).pick;
            } else {
                # if there was a coin above, it will fall to this position.
                $alive = True;
                $coinpos;
            }
        }
        # let the coin dispenser blink and turn it off if we run out of coins
        if @coins[0].defined {
            @coins[0] = Nil
        } elsif --$coins &gt; 0 {
            @coins[0] = 0
        }

        # smooth out the two halfsteps of the animation
        my $start-time;
        ENTER { $start-time = now }
        my $wait-time = now - $start-time;

        sleep 0.1 - $wait-time if $wait-time &lt; 0.1;
        for @coin-icons.keys {
            sleep $wait-time max 0.1;
            display-board(@coins, @stats, $_);
        }
    }
}

sub MAIN($coins = 20, $peg-lines = 6) {
    $row-count = $peg-lines;
    simulate($coins);
}

</code></pre>

<h2 id="main">Main</h2>

<hr />

<pre><code class="language-perl6"> #!/usr/bin/env perl6

    constant @months = &lt;January February March April May June July
                        August September October November December&gt;;
    constant @days = &lt;Su Mo Tu We Th Fr Sa&gt;;


    sub center(Str $text, Int $width) {
        my $prefix = ' ' x ($width - $text.chars) div 2;
        my $suffix = ' ' x $width - $text.chars - $prefix.chars;
        return $prefix ~ $text ~ $suffix;
    }

    sub MAIN(:$year = Date.today.year, :$month = Date.today.month) {
        my $dt = Date.new(:year($year), :month($month), :day(1) );
        my $ss = $dt.day-of-week % 7;
        my @slots = ''.fmt(&quot;%2s&quot;) xx $ss;

        my $days-in-month = $dt.days-in-month;
        for $ss ..^ $ss + $days-in-month {
            @slots[$_] = $dt.day.fmt(&quot;%2d&quot;);
            $dt++
        }

        my $weekdays = @days.fmt(&quot;%2s&quot;).join: &quot; &quot;;
        say center(@months[$month-1] ~ &quot; &quot; ~ $year, $weekdays.chars);
        say $weekdays;
        for @slots.kv -&gt; $k, $v {
            print &quot;$v &quot;;
            print &quot;\n&quot; if ($k+1) %% 7 or $v == $days-in-month;
        }
    }

     # April 2014
# Su Mo Tu We Th Fr Sa
       # 1  2  3  4  5
 # 6  7  8  9 10 11 12
# 13 14 15 16 17 18 19
# 20 21 22 23 24 25 26
# 27 28 29 30
</code></pre>

<pre><code class="language-perl6">﻿# Perl 6 supports writing a MAIN subroutine that is invoked at startup.
# Automatically maps arguments to parameters and generates usage instructions.


sub MAIN($number, Bool :$upto) {
    my @fib = 1, 1, *+* ... Inf;
    if $upto {
    say join ',', @fib[0 ..^ $number];
    }
    else {
        say @fib[$number - 1];
    }
}

#`(
&gt; perl6 MAIN.p6 10
55

&gt; perl6 MAIN.p6 10 --upto
Usage:
  MAIN.p6 [--upto] &lt;number&gt;

# 可选参数写在必选参数前面
&gt; perl6 MAIN.p6 --upto 10
1,1,2,3,5,8,13,21,34,55
)

</code></pre>

<pre><code class="language-perl6">use v6;
sub MAIN($file1, $file2) {
    my $words1 = bag slurp($file1).comb(/\w+/).map(*.lc);
    my $words2 = set slurp($file2).comb(/\w+/).map(*.lc);
    my $unique = ($words1 (-) $words2);
    for $unique.list.sort({ -$words1{$_} })[^10] -&gt; $word {
        say &quot;$word: { $words1{$word} }&quot;;
    }
}
</code></pre>

<h2 id="types">Types</h2>

<hr />

<h2 id="sort">Sort</h2>

<hr />

<pre><code class="language-perl6">use v6;
my %hash = 'Perl'=&gt;80,
         'Python'=&gt;100,
             'Go'=&gt;95,
            'CMD'=&gt;20,
            &quot;Php&quot;=&gt;80,
           &quot;Java&quot;=&gt;85;

for %hash.sort({-.value}).hash.keys -&gt; $key {
    say $key, &quot;\t&quot;, %hash{&quot;$key&quot;}
}

</code></pre>

<pre><code class="language-perl6">use v6;

my %grade = &quot;example.txt&quot;.IO.lines.map: {
    m:s/^(\w+) (&lt;[A..F]&gt;&lt;[+-]&gt;?)$/
        or die &quot;Can't parse line '$_'&quot;;

    ~$0 =&gt; ~$1;
}

say %grade;

say &quot;Zsófia's grade: %grade&lt;Zsófia&gt;&quot;;
say &quot;List of students with a failing grade:&quot;;

say &quot; &quot; ~%grade.grep(*.value ge 'F')».key.join(&quot; &quot;);

say &quot;Distribution of grades by letter:&quot;;
say &quot;  {.key}: {+.value} student{&quot;s&quot; if .value != 1}&quot;
    for %grade.classify(*.value.comb[0]).sort(*.key);

</code></pre>

<pre><code class="language-perl6"># Sort an array of composite structures by a key. For example, if you define a composite structure that presents a name-value pair (in pseudocode):
# Define structure pair such that:
   # name as a string
   # value as a string


# and an array of such pairs:
# x: array of pairs


# then define a sort routine that sorts the array x by the key name.

# This task can always be accomplished with Sorting Using a Custom Comparator. If your language is not listed here, please see the other article.

# Works with: Rakudo Star version 2010.08

my class Employee {
   has Str $.name;
   has Rat $.wage;
}

my $boss     = Employee.new( name =&gt; &quot;Frank Myers&quot;     , wage =&gt; 6755.85 );
my $driver   = Employee.new( name =&gt; &quot;Aaron Fast&quot;      , wage =&gt; 2530.40 );
my $worker   = Employee.new( name =&gt; &quot;John Dude&quot;       , wage =&gt; 2200.00 );
my $salesman = Employee.new( name =&gt; &quot;Frank Mileeater&quot; , wage =&gt; 4590.12 );

my @team = $boss, $driver, $worker, $salesman;

my @orderedByName = @team.sort( *.name )&gt;&gt;.name;
my @orderedByWage = @team.sort( *.wage )&gt;&gt;.name;

say &quot;Team ordered by name (ascending order):&quot;;
say @orderedByName.join('  ');
say &quot;Team ordered by wage (ascending order):&quot;;
say @orderedByWage.join('  ');

# this produces the following output:
# Team ordered by name (ascending order):
# Aaron Fast   Frank Mileeater   Frank Myers   John Dude
# Team ordered by wage (ascending order):
# John Dude   Aaron Fast   Frank Mileeater   Frank Myers


# Note that when the sort receives a unary function, it automatically generates an appropriate comparison function based on the type of the data.

</code></pre>

<pre><code class="language-perl6">class Student {
    has Str $.name;
    has Int $.grade is rw;
}

my $stu1 = Student.new(name =&gt; &quot;zhangwuji&quot;, grade =&gt; 124);
my $stu2 = Student.new(name =&gt; &quot;yangguo&quot;,   grade =&gt; 128);
my $stu3 = Student.new(name =&gt; &quot;zhaomin&quot;,   grade =&gt; 145);
my $stu4 = Student.new(name =&gt; &quot;sunyizhe&quot;,  grade =&gt; 145);
my $stu5 = Student.new(name =&gt; &quot;zhouziruo&quot;, grade =&gt; 128);
my $stu6 = Student.new(name =&gt; &quot;qiaofeng&quot;,  grade =&gt; 124);

my @students = $stu1, $stu2,$stu3,$stu4,$stu5,$stu6;

# classify
# classify 返回一个散列
for @students.classify( *.grade ).sort -&gt; $group {
    say &quot;These students got grade $group.key():&quot;;
    say .name for $group.value.list;
}


# sort
my @c = @students.sort: -&gt; $a, $b {$a.grade &lt;=&gt; $b.grade};
say @c.perl;
</code></pre>

<pre><code class="language-perl6"> use v6;

 my $file = open 'scores';
 my @names = $file.get.words;

 my %matches;
 my %sets;

 for $file.lines -&gt; $line {
     my ($pairing, $result) = $line.split(' | ');
     my ($p1, $p2) = $pairing.words;
     my ($r1, $r2) = $result.split(':');

    %sets{$p1} += $r1;
    %sets{$p2} += $r2;

    if $r1 &gt; $r2 {
        %matches{$p1}++;
    } else {
        %matches{$p2}++;
    }
 }

 my @sorted = @names.sort({ %sets{$_} }).sort({ %matches{$_} }).reverse;

 for @sorted -&gt; $n {
 say &quot;$n has won %matches{$n} matches and %sets{$n} sets&quot;;
 }
</code></pre>

<h2 id="subset">Subset</h2>

<hr />

<ul>
<li>限制字符串长度</li>
</ul>

<pre><code class="language-perl6">   subset NonEmptyString
       of Str
       where *.chars &gt; 0; # 可以把约束条件写到多行

   sub firstName(NonEmptyString $name) {
       say &quot;your name is $name&quot;;
   }

   firstName('Larry');
   firstName('');
</code></pre>

<p>输出：</p>

<pre><code class="language-text">your name is Larry
Constraint type check failed for parameter '$name'
  in sub firstName at subset.p6:5
  in block &lt;unit&gt; at subset.p6:10
</code></pre>

<ul>
<li>限制值域</li>
</ul>

<pre><code class="language-perl6">subset PointLimit of Int where -10 &lt;= * &lt;= 10;
sub test(PointLimit $number) {
    say $number;
}
test(-5); # -5

subset SmallInt of Int where -10 .. 10;
sub small(SmallInt $number) {
    say $number;
}
small(8);


</code></pre>

<ul>
<li>检测密码是否合法</li>
</ul>

<pre><code class="language-perl6">use v6;
# 安全的密码
# 至少 8 位
# 包含大写字母、小写字母
# subset 不能使用 set(*.comb)  形式？

subset Password of Str where *.chars &gt;=8 &amp;&amp; any('A'..'Z','a'..'z') ∈ *.comb.Set;

sub passwordCheck(Password $password) {
    say &quot;Password is Valid&quot;;
}

passwordCheck(&quot;abcdABCD&quot;);

</code></pre>

<ul>
<li>检测密码是否有效并提醒</li>
</ul>

<pre><code class="language-perl6">use v6;

subset Length8    of Str where *.chars &lt; 8;
subset UpCase     of Str where none('A'..'Z') ∈ *.comb.Set;
subset LowerCase  of Str where none('a'..'z') ∈ *.comb.Set;
subset IntNumber  of Str where none('0'..'9') ∈ *.comb.Set;

my $guess = prompt('Enter your password:');

given $guess {
    when Length8   { say '密码长度必须为 8 位 以上'; proceed }
    when  UpCase   { say '密码必须包括大写字母';     proceed }
    when LowerCase { say '密码必须包含小写字母';     proceed }
    when IntNumber { say '密码必须包含数字';                 }
}
</code></pre>

<p>该程序具有可扩展性， 要增加一种密码验证， 只有添加一个 subset 就好了，然后在 given/when 里面增加一个处理。
proceed 相当于 continue， 不像 C 里面的 falling through， Perl 6 里面的 proceed 在继续执行 when 语句时会计算 when 后面的条件</p>

<p>when 可以作为语句修饰符单独使用</p>

<pre><code class="language-perl6">doit() when 42
</code></pre>

<p>等价于</p>

<pre><code class="language-perl6">doit() when $_ ~~ 42
</code></pre>

<p>这在列表解析里面很有用</p>

<pre><code class="language-perl6">my @lucky = ($_ when /7/ for 1..100);
7 17 27 37 47 57 67 70 71 72 73 74 75 76 77 78 79 87 97

</code></pre>

<pre><code class="language-perl6">subset NonEmptyString
    of Str
    where *.chars &gt; 0;

sub firstName(NonEmptyString $name) {
    say &quot;your name is $name&quot;;
}

firstName('Larry');
firstName('');


</code></pre>

<h2 id="traits">Traits</h2>

<hr />

<pre><code class="language-perl6">sub add (Int $inputA, Int $inputB --&gt; Int)
{
    my $result = $inputA+$inputB;

    say $result;         # Oops, this is the last statement, so its return value is used for the subroutine
}

# my $sum = add(5,6);
# Type check failed for return value; expected 'Int' but got 'Bool'

sub add2 (Int $inputA, Int $inputB --&gt; Int)
{
    my $result = $inputA+$inputB;

    return $result;
}

my $sum2 = add2(5,6);
say $sum2;

sub add3 (Int $inputA, Int $inputB) returns Int
{
    my $result = $inputA+$inputB;

    return $result;
}

my $sum3 = add3(5,6);
say $sum3;

</code></pre>

<pre><code class="language-perl6">sub fib(Int $nth where * &gt;= 0) {
  given $nth {
    when 0 { 0 }
    when 1 { 1 }
    default { fib($nth-1) + fib($nth-2) }
 }
}
say fib(3);
#say fib(-3);

subset FirstName
    of Str
  where 0 &lt; *.chars &amp;&amp; *.chars &lt; 256;

sub first_name(FirstName $name){
    say &quot;$name&quot;;
}

first_name(&quot;Wall&quot;);

subset PointLimit of Int where -10 &lt;= * &lt;= 10;
sub test(PointLimit $number) {
    say $number;
}
test(-5);

subset SmallInt of Int where -10 .. 10;
sub small(SmallInt $number) {
    say $number;
}

small(8);

</code></pre>

<pre><code class="language-perl6">multi sub trait_mod:&lt;is&gt;(Routine $r, :$doubles!) {
    $r.wrap({
        2 * callsame;
    });
}

sub square($x) is doubles {
    $x * $x;
}

say square 3;

</code></pre>

<pre><code class="language-perl6">my @list of Int = 1..10000;
say @list[99].WHAT;

</code></pre>

<pre><code class="language-perl6">sub divide(Int $a,
           Int $b where { $^n != 0 }) {
    return $a / $b;
}
say divide(120, 3); # 42
# say divide(100, 0); # Type check failure

# Here is an example of using subtypes to distinguish between two candidates

multi say_short(Str $x) {
    say $x;
}
multi say_short(Str $x
                  where { $_.chars &gt;= 12 }) {
   say substr($x, 0, 10) ~ '...';
}
say_short(&quot;Beer!&quot;);         # Beer!
say_short(&quot;BeerBeerBeer!&quot;); # BeerBeerBe...

</code></pre>

<pre><code class="language-perl6"># Typed Parameters Can restrict a parameter to only accept arguments of a certain type.
sub show_dist(Str $from, Str $to, Int $kms) {
    say &quot;From $from to $to is $kms km.&quot;;
}
show_dist('Copenhagen', 'Beijing', 7305);
show_dist(7305, 'Copenhagen', 'Beijing');

</code></pre>

<h2 id="twigils">Twigils</h2>

<hr />

<pre><code class="language-perl6"> class Point {
        has $.x;
        has $.y;

        method Str() {
           return ($.x, $.y); # 注意我们这次使用 . 而不是 !
        }
    }

my $p = Point.new(x=&gt;10,y=&gt;20);
my ($height,$wide) = $p.Str();
say &quot;高度:$height&quot;;
say &quot;宽度:$wide&quot;;


 class SaySomething {
        method a() { say &quot;a&quot;; }
        method b() { say $.a; }
    }

    SaySomething.a; # prints &quot;a&quot;
    SaySomething.b;

</code></pre>

<pre><code class="language-perl6">my @fave_foods = &lt;hanbao pingguo TV&gt;;
for @fave_foods -&gt; $food {
    say &quot;Jonathan likes to eat $food&quot;;
}
# The bit between the curly braces is done for each thing in the array
# -&gt; $name means “declare $name and put the current thing into it”

# $^identifier 变量用于块中:

my @str = &lt;a very long but shorthand really&gt;;
my @sorted = sort { $^a.chars &lt;=&gt; $^b.chars}, @str;
say @sorted;

# sort 可以更简洁
my @s = sort { .chars }, @str;
say @s;

my $block = {
    $^a + $^b;
};
say $block(1,99);

</code></pre>

<pre><code class="language-perl6">use v6;
# Twigils影响变量的作用域。请记住, twigils 对基本的魔符插值没有影响，那就是:
# 如果  $a 内插， $^a, $*a, $=a, $?a, $.a, 等等也会内插. 它仅仅取决于 $.

    my $lexical   = 1;
    my $*dynamic1 = 10;
    my $*dynamic2 = 100;

    sub say-all() {
        say &quot;$lexical, $*dynamic1, $*dynamic2&quot;;
    }

    # prints 1, 10, 100
    say-all();

    {
        my $lexical   = 2;
        my $*dynamic1 = 11;
        $*dynamic2    = 101;

        # prints 1, 11, 101 ,why 2, 11 ,101?
        # $lexical isn't looked up in the caller's scope but in the scope &amp;say-all was defined in.
        # The two dynamic variables are looked up in the callers scope and therefore have the values 11 and 101.
        # 翻译过来就是, $lexical 不是在调用者的作用域内被查找, 而是在 &amp;say-all 被定义的作用域那儿
        # 也就是第一行的 $lexical = 1 了. 另外两个动态作用域变量在调用者的作用域内被查找, 所以值为 11 和 101
        say-all();
    }

    # prints 1, 10, 101
    say-all();

</code></pre>

<pre><code class="language-perl6">use v6;

# ? twigil 编译常量
say &quot;$?FILE: $?LINE&quot;; # wenhao.p6: 4

</code></pre>

<pre><code class="language-perl6"># ^ twigil 为block 块 或 子例程 申明了一个形式位置参数.
# 形如 $^variable 的变量是一种占位变量. 它们可用在裸代码块中来申明代码块的形式参数. 看下面代码中的块:

for ^4 {
    say &quot;$^seconds follows $^first&quot;;
}

# 1 follows 0
# 3 follows 2

</code></pre>

<h2 id="whatever">Whatever</h2>

<hr />

<p>当作为一个项使用时， 我们把 * 叫做 &ldquo;whatever&rdquo;
 当不是实际值时，它用作占位符
 例如, 1, 2, 3 &hellip; *
 意思是没有终结点的自然数序列</p>

<p>Whatever 闭包
 Whatever 最强大的用处是 Whatever 闭包</p>

<p>对于 whatever 没有特殊意义的普通操作符：
     把 whatever 当作参数传递时就创建了一个闭包！
 所以，举个例子，  <code>* + 1</code> 等价于 <code>-&gt; $a { $a + 1 }</code>
 <code>* + *</code> 等价于 <code>-&gt; $a, $b { $a + $b }</code></p>

<pre><code class="language-perl6">@list.grep(* &gt; 10) # 返回 @list 数组中所有大于 10 的元素
@list.map(* + *)   # 返回 @list 中每对儿的和
</code></pre>

<p>如果给 @a[ ] 的方括号里面传递一个闭包， 它会把 @a 数组的元素个数作为参数传递并计算！</p>

<ul>
<li>数组的最后一个元素</li>
</ul>

<pre><code class="language-perl6">my @a =  1,22,33,11;
say @a[*-1];
say @a[-&gt;$a {$a-1}]; # $a  即为数组@a 的元素个数
</code></pre>

<ul>
<li>数组的倒数第二个元素</li>
</ul>

<pre><code class="language-perl6">say @a[*-2];
say @a[-&gt;$a {$a-2}];
</code></pre>

<p>所以 <code>@a[*/2]</code> 是 @a 数组的中间元素
<code>@a[1..*-2]</code>  是@a 中不包含首尾元素的其它元素
<code>1, 1, * + * ... *</code>  是一个无限列表
* + * 是后来值的生成规则， 最后一个 * 表示没有终结测试。</p>

<pre><code class="language-perl6"># 把闭包存储在标量中
my $a = -&gt; $b { $b + 1 }
$a(3) # 4
</code></pre>

<p>Perl 6 的列表求值是惰性的
只要你不要求最后一个元素， 无限列表是没问题的。
使用绑定 (:=) 操作符把列表赋值给变量：</p>

<pre><code class="language-perl6">    my @fib := 1, 1, * + * ... *
</code></pre>

<p>如果我稍后要 @fib[40] 的值， 会生成足够多的元素以获取数组的第 41 个元素
那些生成的元素会被记忆。
尽管未来， 如果列表未绑定给变量， 之前的值会被忘记
大部分 Perl 6 列表函数能作用并生成惰性列表
@a.map 和 @a.grep 都生成惰性列表， 即使 @a 不是惰性的。
@fib.grep(* %% 2) 是一个偶数惰性列表，例如
@fib Z @a 生成一个惰性列表： @fib[0], @a[0], @fib[1], @a[1] &hellip;
给 for 循环传递一个无限列表是没问题的， 它会循环直到停止。</p>

<h2 id="when">When</h2>

<hr />

<pre><code class="language-perl6"># given 和 when：
# given 接收一个参数和一个 block
# given 把它接收的参数设置为 $_, 然后调用后面的 block
# when 也接收一个参数和一个 block
# when 将 $_ 和 when 接收的参数进行智能匹配
# 如果结果是 True， 则执行代码块
# 然后控制就跳出 when 的包围圈

my $ticks = 0;
given $ticks {
    when 1   {say &quot;&quot;;      }
    when 1/2 {say &quot;/&quot;;     }
    when Int {say $_.Str;  }
    when Rat {say $_.perl; }
    die &quot;Duration must be Int or Rat, but it's { $_.WHAT }&quot;;
}

# given 和 when 可以单独使用
# 设置了 $_,  进行一系列操作时，given 比较方便
# 当 $_ 被设置后 ， when 可以用在任何地方
# when 在 for 循环中很方便

my $boring-lines = 0;
for $*IN.lines {
    when /&quot;Lunasa&quot; | &quot;Altan&quot;/ { say &quot;Found band!&quot;;       }
    when /&quot;fiddle&quot; | &quot;flute&quot;/ { say &quot;Found instrument!&quot;; }
    $boring-lines++;
}


</code></pre>

<h2 id="smart-matching">Smart-Matching</h2>

<hr />

<pre><code class="language-perl6">    constant A = 100;
    constant B = 100;

    my (%powers, %count);

    # find bases which are powers of a preceeding root base
    # store decomposition into base and exponent relative to root
    for 2..Int(sqrt A) -&gt; \a {
        next if a ~~ %powers;
        %powers{a, a**2, a**3 ...^ * &gt; A} = a X=&gt; 1..*;
    }

    # count duplicates
    for %powers.values -&gt; \p {
        for 2..B -&gt; \e {
            # raise to power \e
            # classify by root and relative exponent
            ++%count{p.key =&gt; p.value * e}
        }
    }

    # add +%count as one of the duplicates needs to be kept
    say (A - 1) * (B - 1) + %count - [+] %count.values;
</code></pre>

<pre><code class="language-perl6">use v6;
my %lilei     ='Math'=&gt;98,'Chinese'=&gt;'72','English'=&gt;'128';
my %hanmeimei ='Math'=&gt;98,'Chinese'=&gt;'72','English'=&gt;'128';
say &quot;they have the same course&quot; if %lilei.keys ~~ %hanmeimei.keys;
say 'true' if %lilei{%hanmeimei.keys} ~~ %hanmeimei.values;

my $a = 2
say so $a ~~ 1..3
say so $a ~~ Int
say so $a ~~ 23
say so $a ~~ {$_.Str ne $_.perl}
say so (1..3).ACCEPTS($a)

</code></pre>

<h2 id="regexes">Regexes</h2>

<hr />

<pre><code class="language-perl6"> use v6;
 print 'ok' if '1,2,3' ~~ / \d+ % ',' /; # ok
</code></pre>

<pre><code class="language-perl6"># caps 方法返回所有的捕获，命名的和位置的，按照它们匹配的文本在原始字符串中出现的顺序返回。返回的值是一个 Pair 对象列表。键是捕获的名字或数量，键值是对应的 Match 对象。

if 'abc' ~~ m/(.) &lt;alpha&gt; (.) / {
            say $/.caps.WHAT; # (Parcel)
            my @a = $/.caps;  
            say @a;           # 0 =&gt; ｢a｣ alpha =&gt; ｢b｣ 1 =&gt; ｢c｣ ( 0 =&gt; ｢a｣ 是一个 Pair 对象
            say &quot; -&gt; 这次匹配有  @a.elems() 个 Pair&quot;;
               for $/.caps {
                    say .key, ' =&gt; ', .value.Str; # 键值是 对应的Match 对象, 需要调用 Str 方法, 得到字符串.

             }
 }

 # Output:
 #          0 =&gt; a
 #      alpha =&gt; b
 #          1 =&gt; c


#  复习下 Parcel
# Parcel 由 () &lt;&gt;  逗号分割的列表, 或其它引用结构
# ()
# 1,2,3
# &lt;a b c&gt;
# &lt;&lt;a b c&gt;&gt;
# qw/a b c/

</code></pre>

<pre><code class="language-perl6">my $s = 'the quick brown fox jumped over the the lazy dog';
if $s ~~ m/ &lt;&lt; (\w+) \W+ $0 &gt;&gt; / { # if 不再需要圆括号
    say &quot;Found '$0' twice in a row&quot;;
    say &quot;Found '$/[0]' twice in a line&quot; # $/[0]  可以简写为 $0
}

</code></pre>

<pre><code class="language-perl6"> my $ingredients = 'milk, flour, eggs and sugar';
 # prints &quot;milk, flour, eggs&quot;, 如果 say $/[0] 只会打印 || , 因为[] 是非捕获组!
 $ingredients ~~ m/ [\w+]+ % [\,\s*] / &amp;&amp; say &quot;|$/|&quot;;
# |milk, flour, eggs|
# 这里 \w+ 匹配一个单词，并且 [\w+]+ % [\,\s*]  匹配至少一个单词，并且单词之间用逗号和任意数量的空白分隔。
 '1,2,3' ~~ / \d+ % ',' / &amp;&amp; say &quot;|$/|&quot;;
# |1,2,3|
# %必须要跟在量词后面，否则报错。
# 在 [\w+] 里面 [ ] 是非捕获组

</code></pre>

<pre><code class="language-perl6"># 如果你在捕获后面加上量词，匹配对象中的对应的项是一列其它对象：

use v6;
my $ingredients = 'eggs, milk, sugar and flour';

if $ingredients ~~ m/(\w+)+ % [\,\s*] \s* 'and' \s* (\w+)/ {
    say 'list: ', $/[0].join(' | ');
    say 'end: ', &quot;$/[1]&quot;;
    say $/.elems; # 数组 $/ 中含有 2 个元素
    say $/[0].WHAT;  # ARRAY, 第一个捕获 $/[0] 其实是一个数组
    say $/[0].elems; # 3, 第一个 (\w+) 匹配了 3 次
}

# 这打印:
# list: eggs | milk | sugar
# end: flour

#  第一个捕获(\w+)被量词化了，所以$/[0]包含一列单词。代码调用 .join方法将它转换为字符串。
#  不管第一个捕获匹配了多少次（并且有$/[0]中有多少元素），第二个捕获$/[1]始终可以访问。

</code></pre>

<pre><code class="language-perl6">use v6;
my $str = 'Germany was reunited on 1990-10-03, peacefully';

if $str ~~ m/ (\d**4) \- (\d\d) \- (\d\d) / {
    say $/.WHAT;  # Match
    say $/.elems; # 3
    say 'Year: ',&quot;$/[0]&quot;;
    say 'Month: ',&quot;$/[1]&quot;;
    say 'Day: ',&quot;$/[2]&quot;;

    # usage as an array:
    say $/.join('-'); # prints 1990-10-03
}

# Year: 1990
# Month: 10
# Day: 03
# 1990-10-03
</code></pre>

<pre><code class="language-perl6">my $s = 'the quick brown fox jumped over the the lazy dog';
my regex word { \w+ [ \' \w+ ]?              }
my regex dup  { « &lt;danci=&amp;word&gt; \W+ $&lt;danci&gt; » } # 要使用 &amp;name 调用正则, 就像调用子例程一样 &amp;sub , 调用后的结果起名为 danci, 就像给子例程起名字一样
if $s ~~ m/ &lt;dupword=&amp;dup&gt; / {
    say &quot;Found '{$&lt;dupword&gt;&lt;danci&gt;}' twice in a row&quot;;
    # say $/.keys(); # dupword, 获取散列的键
    say $/;
}

# 这段代码引入了一个名为 word 的正则表达式，它至少匹配一个单词字符，后面跟着一个可选的单引号和更多的单词字符。
# 另外一个名为 dup （duplcate的缩写，副本的意思）的正则包含一个单词边界锚点。

# 在正则里面，语法 &lt;&amp;word&gt; 在当前词法作用域内查找名为word的正则并匹配这个正则表达式。
# &lt;name=&amp;regex&gt; 语法创建了一个叫做 name 的捕获，它记录了 &amp;regex 匹配的内容。

# 在这个例子中，dup 调用了名为 word 正则，随后匹配至少一个非单词字符，之后再匹配相同的字符串（ 前面word 正则匹配过的）一次，它以另外一个字符边界结束。这种向后引用的语法记为美元符号 $  后面跟着用尖括号包裹着捕获的名字。

# 在 if 代码块里， $&lt;dupword&gt; 是  $/{'dupword'} 的快捷写法。因为 $/ 是一个特殊的散列, 所以可以通过键 {'dupword'} 访问到散列的值. 它访问正则 dup 产生的匹配对象。
# dup 也有一个叫 danci 的子规则。从那个调用产生的匹配对象用 $&lt;dupword&gt;&lt;danci&gt;来访问。

# 直接打印 $/ 的结果, $/ 这里又变成了一个特殊的散列, fuck, 上次它不是一个特殊的数组吗? 百变星君啊,擦!
#

# ｢the the｣
#  dupword =&gt; ｢the the｣
#   danci =&gt; ｢the｣

</code></pre>

<pre><code class="language-perl6"> use v6;
 my $ingredients = 'eggs, milk, sugar and flour';

 if $ingredients ~~ m/(\w+)+ % [\,\s*] \s* 'and' \s* (\w+)/ {
 say 'list: ', $/[0].join(' | ');
 say 'end: ', &quot;$1&quot;;
 }
</code></pre>

<pre><code class="language-perl6"> use v6;
 my $str = 'Germany was reunited on 1990-10-03, peacefully';

 if $str ~~ m/ (\d**4) \- (\d\d) \- (\d\d) / {
 say 'Year: ',&quot;$/[0]&quot;;
 say 'Month: ',&quot;$/[1]&quot;;
 say 'Day: ',&quot;$/[2]&quot;;
 # usage as an array:
 say $/.join('-'); # prints 1990-10-03
 }

</code></pre>

<pre><code class="language-perl6">my regex insigline {
^ \s* [ &lt;?&gt; | '#' .* | '{' | '}' ] \s* $
}
  sub MAIN(*@files) {
      my $total = 0;
      for @files -&gt; $filename {
           try {
           $total += lines($filename.IO).grep(
              { $_ !~~ /&lt;&amp;insigline&gt;/ } ).elems;
      }
     note &quot;can't read $filename &quot; if $!;
   }
say $total;
}

</code></pre>

<pre><code class="language-perl6">use v6;
 my $ingredients = 'eggs, milk, sugar and flour';

 if $ingredients ~~ m/:s ( \w+ )+ % \,'and' (\w+)/ {
 say 'list: ', $/[0].join(' | ');
 say 'end: ', &quot;$/[1]&quot;;
 }
</code></pre>

<h2 id="subroutine">Subroutine</h2>

<hr />

<p><code>--&gt;</code> 在 Perl 6 中是什么意思？
<code>--&gt;</code> 就是指定返回值的类型</p>

<pre><code class="language-perl6">sub add (Int $inputA, Int $inputB --&gt; Int)
{
    my $result = $inputA+$inputB;

    say $result; # this is the last statement, so its return value is used for the subroutine
}

my $sum = add(5,6);
# Type check failed for return value; expected 'Int' but got 'Bool'


sub add2 (Int $inputA, Int $inputB --&gt; Int)
{
    my $result = $inputA+$inputB;

    return $result;
}

my $sum2 = add2(5,6);
say $sum2;
</code></pre>

<p>使用  <code>return</code> 约束更清晰：</p>

<pre><code class="language-perl6">sub add3 (Int $inputA, Int $inputB) returns Int
{
    my $result = $inputA+$inputB;

    return $result;
}

my $sum3 = add3(5,6);
say $sum3;
</code></pre>

<h2 id="signatures">Signatures</h2>

<hr />

<p>签名的几种形式：</p>

<pre><code class="language-perl6">sub optional               {...}
sub basic($foo)            {...}
sub default($foo = 3)      {...}
sub named(:$name)          {...}
sub typed(Bool $is_foo)    {...}
sub constraint(Str $name
    where *.chars &gt; 0)     {...}
</code></pre>

<pre><code class="language-perl6">﻿use v6;

my @a=1,2,3;
my $s='Escape Plan';
my %h='Rakudo'=&gt;'Star','STD'=&gt;'Larry';

# 捕获就是一系列实参的签名
my $capture = \(@a,$s,%h);      # creating a capture, &quot;\&quot; was free since there are no references anymore
say(|$capture).perl;            # flatten into argument list (hash like context)
# ||$cap;                       # flatten into semicolon list (array like context)

</code></pre>

<pre><code class="language-perl6">﻿use v6;

# A set of parameters form a signature. 一组形参组成签名
# A set of arguments form a capture.    一组实参组成捕获

sub greet($name, :$greeting = 'Hi') {
    say &quot;$greeting, $name!&quot;;
}
greet('Лена', greeting =&gt; 'Привет');
</code></pre>

<pre><code class="language-perl6">sub rectangle(:$width!, :$height!, :$char = 'X') {
    say $char x $width for ^$height;
}

rectangle char =&gt; 'o', width =&gt; 8, height =&gt; 4;
rectangle :width(20), :height&lt;5&gt;;

</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Sometimes, you need to do some more powerful validation on arguments.

sub discount($price, $percent
             where (1 &lt;= $percent &lt;= 100)) {
    say &quot;You get $percent% off! Pay EUR &quot; ~ $price - ($price * $percent / 100);
}
discount(100, 20);
discount(100, 200);
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Be careful about using type constraints on arrays and hashes. The type constraints the elements.
# 在对数组和散列使用类型限制时要小心. 类型限制的是元素!

sub total(Array @distances) { # 限制数组 @distances 中的每个元素为数组.
    # WRONG! Takes an Array of Arrays!
}

sub total(Int @distances) {
    # Correct, takes an array of Ints.
}
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Dispatch By Arity(he number of arguments that a function can take)
# Example (from Test.pm): dispatch by different number of parameters

multi sub todo($reason, $count) is export {
    $todo_upto_test_num = $num_of_tests_run + $count;
    $todo_reason = '# TODO ' ~ $reason;
}

multi sub todo($reason) is export {
    $todo_upto_test_num = $num_of_tests_run + 1;
    $todo_reason = '# TODO ' ~ $reason;
}
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Can use multiple dispatch with constraints to do a lot of &quot;write what you know&quot; style solutions

# Factorial:
# fact(0) = 1
# fact(n) = n * fact(n - 1)

multi fact(0)  { 1 };
multi fact($n) { $n * fact($n - 1) };

say fact(10);


# Fibonacci Sequence:
# fib(0) = 0
# fib(1) = 1
# fib(n) = fib(n - 1) + fib(n - 2)

# mutil 声明的子例程语句结尾不需要跟分号;
multi fib(0)  { 0 }
multi fib(1)  { 1 }
multi fib($n) { fib($n - 1) + fib($n - 2) }

say fib(10);
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Example: part of a JSON emitter

multi to-json(Array $a) {
    return '[ ' ~
        $a.values.map({ to-json($_) }).join(', ') ~
        ' ]';
}

multi to-json(Hash $h) {
    return '{ ' ~
        $h.pairs.map({
            to-json(.key) ~ ': ' ~ to-json(.value)
        }).join(', ') ~
        ' }';
}
</code></pre>

<pre><code class="language-perl6">sub fst(*@ [$fst]){
    say $fst;
}

fst(1);
fst(1,2);

</code></pre>

<pre><code class="language-perl6">sub is-in(@array, $elem) {
  # this will `return` out of the `is-in` sub
  # once the condition evaluated to True, the loop won't be run anymore
  map({ return True if $_ ==  $elem }, @array);
}

my @array = 1,2,3,4,5;
is-in(@array,3);

</code></pre>

<pre><code class="language-perl6">sub escape ($str) {
    $_ = $str;
    # Puts a slash before non-alphanumeric characters
    s:g[&lt;-alpha-digit&gt;] = &quot;\\$/&quot;;
}

say escape &quot;foobar&quot;;

{
    sub escape ($str) {
        $_ = $str;
        # Writes each non-alphanumeric character in its hexadecimal escape
        s:g[&lt;-alpha-digit&gt;] = &quot;\\x[{ $/.base(16) }]&quot;;
    }

    say escape &quot;foo#bar?&quot;; # foo\x[23]bar\x[3F]
}

# Back to original escape function
say escape &quot;foo#bar?&quot;;
</code></pre>

<pre><code class="language-perl6">sub greet($name, $greeting = 'Ahoj') {
    say &quot;$greeting, $name!&quot;;
}
greet('Anna'); # Ahoj Anna
greet('Лена', 'Привет '); # Привет, Лена&quot;

</code></pre>

<pre><code class="language-perl6">use v6;

# In Perl 6, passing an array or hash works like passing a reference
# 在 Perl 6中, 传递数组或散列就像传递引用那样

sub example(@array, %hash) {
    say @array.elems;
    say %hash.keys.join(', ');
}

my @numbers = 1,2,3,4;
my %ages = Jnthn =&gt; 25, Noah =&gt; 120;
example(@numbers, %ages);
</code></pre>

<pre><code class="language-perl6">use v6;

# Empty list sorts to the empty list
multi quicksort([]) { () }

# Otherwise, extract first item as pivot...
multi quicksort([$pivot, *@rest]) {

    # Partition.
    my @before = @rest.grep(* &lt; $pivot);
    my @after  = @rest.grep(* &gt;= $pivot);
    # Sort the partitions.
    (quicksort(@before), $pivot, quicksort(@after))
}

my @unsorted = &lt;13 1 9 12 4 2015&gt;;
say quicksort(@unsorted); # 1  4  9  12  13  2015
</code></pre>

<pre><code class="language-perl6">use v6;

sub convert_currency($amount, $rate) {
    $amount = $amount * $rate;
    return $amount;
}

sub convert_currency_copy($amount is copy, $rate) {
    $amount = $amount * $rate;
    return $amount;
}

sub convert_currency_rw($amount is rw, $rate) {
    $amount = $amount * $rate;
    return $amount;
}



my $price = 99;
$price = convert_currency($price, 11.1);
$price_copy = convert_currency_copy($price, 11.1);
$price_rw = convert_currency_rw($price, 11.1);

say $price;
say $price_copy;
say $price_rw;

</code></pre>

<pre><code class="language-perl6">use v6;

# In Perl 6, every value knows its type.

say 42.WHAT;
say &quot;camel&quot;.WHAT;
say [1, 2, 3].WHAT;
say (sub ($n) { $n * 2 }).WHAT;

# (Int)
# (Str)
# (Array)
# (Sub)

# A type name in Perl 6 represents all possible values of that type.
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# Sometimes, you want to accept any type, but then transform it into another type before binding to the parameter
# 强制类型转换

sub show_dist($from, $to, $kms as Int) {
   say &quot;From $from to $to is $kms km.&quot;;
}
show_dist('Kiev', 'Lviv', '469');
show_dist('Kiev', 'Lviv', 469.35);
</code></pre>

<pre><code class="language-perl6">use v6;

# Can restrict a parameter to only accept arguments of a certain type.


sub show_dist(Str $from, Str $to, Int $kms) {
    say &quot;From $from to $to is $kms km.&quot;;
}
show_dist('Kiev', 'Lviv', 469);
show_dist(469, 'Kiev', 'Lviv'); #  Error!
</code></pre>

<pre><code class="language-perl6">sub  foo(@array [$fst, $snd]) {
  say &quot;My first is $fst, my second is $snd ! All in all, I'm @array[].&quot;;
  # (^ remember the `[]` to interpolate the array)
}
my @tail = 1,2;
foo(@tail);

 #=&gt; My first is 2, my second is 3 ! All in all, I'm 2 3

</code></pre>

<pre><code class="language-perl6">use v6;

# Can extract values by attribute (only those that are declared with accessors)

sub nd($r as Rat (:$numerator, :$denominator)) {
    say &quot;$r = $numerator/$denominator&quot;;
}
nd(4.2);
nd(3/9);
</code></pre>

<pre><code class="language-perl6">sub slurp-in-array(@ [$fst, *@rest]) { # You could keep `*@rest` anonymous
  say $fst + @rest.elems;   # `.elems` returns a list's length.
                            # Here, `@rest` is `(3,)`, since `$fst` holds the `2`.
}
my @array = &lt;2 3 4 5&gt;;
slurp-in-array(@array);

</code></pre>

<h2 id="built-in-types-and-functions">Built-in types and functions</h2>

<hr />

<h3 id="classify">.classify</h3>

<hr />

<pre><code class="language-perl6">.say for slurp(&quot;README.txt&quot;)\    # whole file into string
         .words()\               # split into list of words
         .classify( *.Str );

my @a = slurp(&quot;README.txt&quot;).words;
.say  for @a.classify( *.Str );

# 输出的是一个散列
# that =&gt; that that
# the  =&gt; the the the the the the the the the
# is   =&gt; is is is
# ......

my %hash = @a.classify( *.Str );
# 输出前 10 个最常见的单词
for %hash.sort({-.value.elems}).hash.kv.[^20] -&gt; $key, $value {
    say $key ,&quot;\t&quot;, $value.elems;

}
</code></pre>

<hr />

<p>分类后，生成一个散列， 键是  分类依据（可以根据字符数，字符等），下面这例是根据字符数分类， 它会把字符数相同的元素归为一类， 键值就是数组里的元素。</p>

<pre><code class="language-perl6">my @names = &lt;Patrick Jonathan Larry Moritz Audrey&gt;;
say .key, &quot;\t&quot;, ~.values
     for @names.classify( *.chars );

# Output:
# 5       Larry
# 6       Moritz Audrey
# 7       Patrick
# 8       Jonathan

.say for slurp(&quot;README&quot;)\             # whole file into string
    .words()\                         # split into list of words
    .classify( *.Str )\               # group words w/ multiplicity
    .map({; .key =&gt; .value.elems })\  # turn lists into lengths
    .sort( { -.value } )\             # sort descending
    .[ ^10 ];                         # 10 most common words

`#(
*.Str
I =&gt; I I I I, On =&gt; On On, a =&gt; a a a, black =&gt; black black black black black, day =&gt; day day day day, love =&gt; love, read =&gt; read, summer =&gt; summer summer, sunny =&gt; sunny sunny, to =&gt; to

 .map({; .key =&gt; .value.elems })\
On =&gt; 2 a =&gt; 3 sunny =&gt; 2 summer =&gt; 2 day =&gt; 4 I =&gt; 4 black =&gt; 5 love =&gt; 1 to =&gt; 1 read =&gt; 1  

 .sort( { -.value } )\
black =&gt; 5 day =&gt; 4 I =&gt; 4 a =&gt; 3 On =&gt; 2 sunny =&gt; 2 summer =&gt; 2 love =&gt; 1 to =&gt; 1 read =&gt; 1  
)
</code></pre>

<pre><code class="language-perl6">class Student {
    has Str $.name;
    has Int $.grade is rw;
}
my $stu1 = Student.new(name =&gt; &quot;zhangwuji&quot;, grade =&gt; 124);
my $stu2 = Student.new(name =&gt; &quot;yangguo&quot;,   grade =&gt; 128);
my $stu3 = Student.new(name =&gt; &quot;zhaomin&quot;,   grade =&gt; 145);
my $stu4 = Student.new(name =&gt; &quot;sunyizhe&quot;,  grade =&gt; 134);

my @students = $stu1, $tu2,$stu3,$stu4;
my Student @students = get-students();

for @students.classify( *.grade ).sort -&gt; $group {
    say &quot;These students got grade $group.key():&quot;;
    say .name for $group.value.list;
}
</code></pre>

<h3 id="pick-and-roll">.pick and .roll</h3>

<hr />

<pre><code class="language-perl6">
say @deck.pick();                   # pick a card, any card...

say @deck.pick(5);                  # poker hand

my @shuffled = @deck.pick(*);       # here, '*' means 'keep going'

my @urn = &lt;black white white&gt;;      # beads, 1/3 black, 2/3 white
.say for @urn.roll(50);             # like .pick, but new each time

for @urn.roll(*) {
    .say;                           # infinite bead selector
}

say [+] (1..6).roll(4);             # 4d6

class Enemy {
    method attack-with-arrows   { say &quot;peow peow peow&quot;    }
    method attack-with-swords   { say &quot;swish cling clang&quot; }
    method attack-with-fireball { say &quot;sssSSS fwoooof&quot;    }
    method attack-with-camelia  { say &quot;flap flap RAWWR!&quot;  }
}

my $selector = { .name ~~ /^ 'attack-with-' / };
given Enemy.new -&gt; $e {
    my $attack-strategy
        = $e.^methods().grep($selector).pick();

    $e.$attack-strategy();           # call a random method
}
</code></pre>

<h3 id="sort-1">.sort</h3>

<hr />

<pre><code class="language-perl6"># 1 if $a is higher, -1 if $b is higher, 0 if equal
$a &lt;=&gt; $b;

# 根据分数排序 students
@students.sort: -&gt; $a, $b { $a.grade &lt;=&gt; $b.grade };

# same thing
@students.sort: { $^a.grade &lt;=&gt; $^b.grade };

# same thing
@students.sort: { .grade };

# same thing
@students.sort: *.grade;

# leg gives -1, 0 or 1 according to lexicographic ordering
# 'leg' is for Str, 'cmp' is now for type-agnostic sort
$a leg $b;

# sort students by name (Unicode order)
@students.sort: { $^a.name leg $^b.name };

# same thing
@students.sort: *.name;

# don't worry, things are properly cached; no re-evaluations
@items.sort: *.expensive-calculation();

# ...which means this works (and is a fair shuffle)
@deck.sort: { rand }

# ...but this is cuter :)
@deck.pick(*);
</code></pre>

<h3 id="操作符重载">操作符重载</h3>

<hr />

<pre><code class="language-perl6">sub infix:&lt;±&gt;($number, $fuzz) {
    $number - $fuzz + rand * 2 * $fuzz;
}

say 15 ± 5;                          # somewhere between 10 and 20

sub postfix:&lt;!&gt;($n) { [*] 1..$n }
say 5!;                              # 120

class Physical::Unit {
    has Int $.kg = 0;                # these attrs denote powers of units
    has Int $.m  = 0;                # eg $.kg == 2 means that this object
    has Int $.s  = 0;                # has a kg**2 unit

    has Numeric $.payload;

    method multiply(Physical::Unit $other) {
        Physical::Unit.new(
            :kg( $.kg + $other.kg ),
            :m( $.m + $other.m ),
            :s( $.s + $other.s ),
            :payload( $.payload * $other.payload )
        )
    }

    method invert() {
        Physical::Unit.new(
            :kg( -$.kg ), :m( -$.m ), :s( -$.s ),
            :payload( 1 / $.payload )
        )
    }

    method Str {
        $.payload
        ~ ($.kg ?? ($.kg == 1 ?? &quot; kg&quot; !! &quot;kg**$.kg&quot;) !! &quot;&quot;)
        ~ ($.m  ?? ($.m  == 1 ?? &quot; m&quot;  !! &quot;m**$.m&quot;)   !! &quot;&quot;)
        ~ ($.s  ?? ($.s  == 1 ?? &quot; s&quot;  !! &quot;s**$.s&quot;)   !! &quot;&quot;)
    }
}

sub postfix:&lt;kg&gt;(Numeric $payload) { Physical::Unit.new( :kg(1), :$payload ) }
sub postfix:&lt;m&gt;(Numeric $payload) { Physical::Unit.new( :m(1), :$payload ) }
sub postfix:&lt;s&gt;(Numeric $payload) { Physical::Unit.new( :s(1), :$payload ) }

# Note how we now use 'multi sub', so as not to shadow the original infix:&lt;*&gt;
multi sub infix:&lt;*&gt;(Physical::Unit $a, $b) {
    $a.clone( :payload($a.payload * $b) );
}

multi sub infix:&lt;*&gt;($a, Physical::Unit $b) {
    $b.clone( :payload($a * $b.payload) );
}

multi sub infix:&lt;*&gt;(Physical::Unit $a, Physical::Unit $b) {
    $a.multiply($b);
}

multi sub infix:&lt;/&gt;(Physical::Unit $a, $b) {
    $a.clone( :payload($a.payload / $b) );
}

multi sub infix:&lt;/&gt;($a, Physical::Unit $b) {
    $b.invert.clone( :payload($a / $b.payload) );
}

multi sub infix:&lt;/&gt;(Physical::Unit $a, Physical::Unit $b) {
    $a.multiply($b.invert);
}

say 5m / 2s;                         # 2.5 m s**-1
say 100kg * 2m / 5s;                 # 40 kg m s**-1
</code></pre>

<h3 id="z">Z</h3>

<hr />

<pre><code class="language-perl6"># Z (the 'zip operator') means &quot;mix these lists together&quot;
my @tastes = &lt;spicy sweet bland&gt;;
my @foods = &lt;soup potatoes tofu&gt;;
@tastes Z @foods; # &lt;spicy soup sweet potatoes bland tofu&gt;

class Student {
    has Str $.name;
    has Int $.grade is rw;
}

my $stu1 = Student.new(name =&gt; &quot;zhangwuji&quot;, grade =&gt; 124);
my $stu2 = Student.new(name =&gt; &quot;yangguo&quot;,   grade =&gt; 128);
my $stu3 = Student.new(name =&gt; &quot;zhaomin&quot;,   grade =&gt; 145);
my $stu4 = Student.new(name =&gt; &quot;sunyizhe&quot;,  grade =&gt; 145);
my $stu5 = Student.new(name =&gt; &quot;zhouziruo&quot;, grade =&gt; 128);
my $stu6 = Student.new(name =&gt; &quot;qiaofeng&quot;,  grade =&gt; 124);

my @students = $stu1, $stu2,$stu3,$stu4,$stu5,$stu6;
# » 为每个元素调用方法
.say for @students».grade;   # all the grades

for @students».name Z @students».grade -&gt; $name, $grade {
    say &quot;$name got a $grade this year&quot;;
}

# Note that the latter list is infinite -- it works anyway
for @students».name Z (1..6).roll(*) -&gt; $name, $roll {
    say &quot;$name rolls a $roll&quot;;
}

# you can also Z together two lists with an infix op
my @total-scores = @first-scores Z+ @second-scores;

# strings as keys, the appropriate number of 1s as values
my %hash = @names Z=&gt; 1 xx *;    # xx is list repeat

# line people up with increasing numbers
my %people2numbers = @people Z=&gt; 1..*;

# don't have a good op? roll your own!
sub infix:&lt;likes&gt;($liker, $likee) { &quot;$liker is fond of $likee&quot; }
# note how the op infix:&lt;Zlikes&gt; is automatically available
my @relations = @likers Zlikes @likees;

</code></pre>

<pre><code class="language-perl6">&gt; sub infix:&lt;likes&gt;($liker, $likee) { &quot;$liker is fond of $likee&quot; }
sub infix:&lt;likes&gt; (Any $liker, Any $likee) { #`(Sub+{Precedence}|140676897934560) ... }
&gt; &quot;aaa&quot; likes &quot;bbb&quot;
aaa is fond of bbb
&gt; my @a = &lt;a b c&gt;;
a b c
&gt; my @b = &lt;1 2 3&gt;;
1 2 3
&gt; @a Zlikes @b
a is fond of 1 b is fond of 2 c is fond of 3
&gt; .say for @a Zlikes @b
a is fond of 1
b is fond of 2
c is fond of 3
</code></pre>

<h3 id="infix">infix:&lt;&hellip;&gt;</h3>

<hr />

<pre><code class="language-perl6">1 ... $n                                    # integers 1 to $n
$n ... 1                                    # and backwards

1, 3 ... $n                                 # odd numbers to $n
1, 3, ... *                                 # odd numbers
1, 2, 4 ... *                               # powers of two
map { $_ * $_ }, (1 ... *)                  # squares

1, 1, -&gt; $a, $b { $a + $b } ... *           # fibonacci
1, 1, { $^a + $^b } ... *                   # ditto
1, 1, *+* ... *                             # ditto

'Camelia', *.chop ... '';                   # all prefixes of 'Camelia'
# Camelia Cameli Camel Came Cam Ca C
# See http://blog.plover.com/CS/parentheses.html
# for the principle behind this
sub next-balanced-paren-string($s) {
    $s ~~ /^ ( '('+ ) ( ')'+ ) '(' /;
    [~] $s.substr(0, $/.from),
        &quot;()&quot; x ($1.chars - 1),
        &quot;(&quot; x ($0.chars - $1.chars + 2),
        &quot;)&quot;,
        $s.substr($/.to);
}

my $N = 3;

my $start = &quot;()&quot; x $N;
my &amp;step = &amp;next-balanced-paren-string;
my $end = &quot;(&quot; x $N ~ &quot;)&quot; x $N;

for $start, &amp;step ... $end -&gt; $string {
    say $string;
}

# Output:
# ()()()
# (())()
# ()(())
# (()())
# ((()))

</code></pre>

<h2 id="chained-comparisons">Chained Comparisons</h2>

<hr />

<h2 id="muti-dispatch">Muti dispatch</h2>

<hr />

<pre><code class="language-perl6">multi foo(Int $x) { 1 }
multi foo(Int $x) is default { 2 }
say foo(1); # 2
</code></pre>

<pre><code class="language-perl6"># Operator overloading in Perl 6 will be done by multi-dispatch routines
# (In fact, all of the built-in operators are invoked by a multi-dispatch.

# Dispatch By Arity
# 􏰀 Arity = number of arguments that a routine takes
#􏰀  Could do the previous example as:

multi sub greet($name) {
    say &quot;Ahoj, $name!&quot;;
}
multi sub greet($name, $greeting) {
    say &quot;$greeting, $name!&quot;;
}
greet('Anna'); # Ahoj Anna
greet('Лена', 'Привет '); # Привет, Лена&quot;

# Type-Based Dispatch
#  􏰀 We can write types in a signature
#􏰀   They are used to help decide which candidate to call

multi sub double(Int $x) {
    return 2 * $x;
}
multi sub double(Str $x) {
    return &quot;$x $x&quot;;
}
say double(21);      # 42
say double(&quot;he&quot;);   # he he

</code></pre>

<pre><code class="language-perl6">multi sub MAIN('send', $filename) {
 ...
}
multi sub MAIN('fetch', $filename) {
 ...
}
multi sub MAIN('compare', $file1, $file2) {
 ...
}

#`(
... 是 yadayadayada 占位符
&gt; perl6 &quot;multiple_MAIN.p6&quot;
Usage:
  multiple _MAIN.p6 send &lt;filename&gt;
  multiple _MAIN.p6 fetch &lt;filename&gt;
  multiple _MAIN.p6 compare &lt;file1&gt; &lt;file2&gt;
)
</code></pre>

<h2 id="module-management">Module management</h2>

<hr />

<h2 id="native-library-calls">Native Library Calls</h2>

<hr />

<h2 id="operators">Operators</h2>

<hr />

<pre><code class="language-perl6">my @suits  = &lt;♣ ♢ ♡ ♠&gt;;
my @ranks  = 2..10, &lt;J Q K A&gt;;

# concatenate each rank with each suit (2♣ 2♢ 2♡ ... A♠)
my @deck = @ranks X~ @suits;

# build a hash of card names to point values
# 52 张牌， 4 种花色， A 的值为 11 ， J、Q、K 为 10
my %points = @deck Z @( (2..10, 10, 10, 10, 11) Xxx 4 );

# 把牌打乱
@deck .= pick(*); # shuffle the deck
my @hand = @deck.splice(0, 5); # 抓取前 5 张牌
say ~@hand; #  显示抓取的是哪 5 张
say [+] %points{@hand}; # 这 5 张牌面的值加起来是多少

</code></pre>

<pre><code class="language-perl6">sub postfix:&lt;!&gt;($n where $n &gt; 0) {
   [*] 2..$n
}
say 5!;

constant fact = 1, [\*] 1..*;
say fact[5];

</code></pre>

<pre><code class="language-perl6">use v6;
my $file = open 'flip_flop.txt';
for $file.lines -&gt; $line {
say $line if !($line ~~ m/^\;/ ff $line ~~ m/^\&quot;/);
}

#`(
flip_flop.txt 内容如下：
; next is some lines to skip,include this line
fuck fuck fuck
dam dam dam
mie mie mie
&quot; next is subject
There is more than one way to do it
                                -- Larry Wall

We hope Perl6 is wrote by the hole Socfilia
                                -- Larry Wall
; next is some lines to skip,include this line
fuck fuck fuck
dam dam dam
mie mie mie
&quot; next is subject
programming is hard,Let's go shopping
                               -- Larry Wall
Ruby is Another Perl6
                               -- Larry Wall
输出：
There is more than one way to do it
                                -- Larry Wall
We hope Perl6 is wrote by the hole Socfilia
                                -- Larry Wall
programming is hard,Let's go shopping
                               -- Larry Wall
Ruby is Another Perl6
                               -- Larry Wall
圣诞节中的例子：

)
for 1..20 {.say if $_==9 ff $_==16}
say '-' x 10;
for 1..20 {.say if $_==9 ^ff $_==16}
say '-' x 10;
for 1..20 {.say if $_==9 ff^ $_==16}
say '-' x 10;
for 1..20 {.say if $_==9 ^ff^ $_==16}


</code></pre>

<h2 id="awesome-operators">Awesome Operators</h2>

<hr />

<p>In Perl 6 we have a few new operators&hellip;</p>

<h2 id="junctions-1">Junctions</h2>

<hr />

<pre><code class="language-perl6">if $status eq 'error' || $status eq 'warning' {
...
}
</code></pre>

<p>在 Perl 6 里面:</p>

<pre><code class="language-perl6">if $status eq 'error' | 'warning' {
...
}
</code></pre>

<p>Perl 5 里面:</p>

<pre><code class="language-perl6">while $value &lt; $limit1 &amp;&amp; $value &lt; $limit2 {
...
}
</code></pre>

<p>Perl 6 里面</p>

<pre><code class="language-perl6">while $value &lt; $limit1 &amp; $limit2 {
...
}
</code></pre>

<ul>
<li>Sequences</li>
</ul>

<pre><code class="language-perl6">say 1, 2, 4 ... 1024;
1 2 4 8 16 32 64 128 256 512 1024

my @fib = 1, 1, *+* ... *;
say @fib[0..9]
1 1 2 3 5 8 13 21 34 55 89
</code></pre>

<ul>
<li>^ (zero up to&hellip;)</li>
</ul>

<pre><code class="language-perl6">my @fib = 1, 1, *+* ... *;
say @fib[^10]
1 1 2 3 5 8 13 21 34 55 89
</code></pre>

<ul>
<li>Awesome Meta-operators</li>
<li>Higher order operators</li>

<li><p>Operators that <strong>operate on operators</strong></p></li>

<li><p>Reduction
Puts an operator between every element in a list</p></li>
</ul>

<pre><code class="language-perl6">say [*] 1..10
3628800

my @sorted = 1,4,7,9,11;
my @unsorted = 3, 1, 9, 25;

say [&lt;] @sorted;
say [&lt;] @unsorted;

Bool::True
Bool::False
</code></pre>

<ul>
<li>Zip
Take elements from two or more lists and combine them with some operator</li>
</ul>

<pre><code class="language-perl6">say 1 .. 6 Z~ 'A'..'F’
1A 2B 3C 4D 5E 6F
</code></pre>

<ul>
<li>Cross</li>
<li>All permutations of two or more lists, combined with some operator</li>
</ul>

<pre><code class="language-perl6">say 1 .. 3 X~ 'A'..'F‘
1A 1B 1C 1D 1E 1F 2A 2B 2C 2D 2E 2F 3A 3B 3C 3D 3E 3F
</code></pre>

<ul>
<li>Your Awesome Operators</li>
</ul>

<p>What if Perl 6 built in operators are not enough?</p>

<p>You can add your own!</p>

<ul>
<li>Factorial
Add a ! operator to do factorial</li>
</ul>

<pre><code class="language-perl6">sub postfix:&lt;!&gt;($n) { [*] 1..$n }
say 10!
3628800
</code></pre>

<pre><code># And you have all of unicode!
# Insert In Middle
## Operator to add an element to the middle of an array
</code></pre>

<ul>
<li>中</li>
</ul>

<pre><code class="language-perl6">sub infix:&lt;中&gt;(@array, $ins) {
    @array.splice(+@array / 2, 0, $ins);
    return @array;
}

my @a = 1,2,4,5;
say @a 中 3;

1 2 3 4 5
</code></pre>

<h2 id="feeds">Feeds</h2>

<hr />

<pre><code class="language-perl6">use v6;
use Test;

{

    my @data = &lt;1 2 4 5 7 8&gt;;
    my @odds = &lt;1 5 7&gt;;

    eval_dies_ok('@data &lt;== grep {$_ % 2} &lt;== @data', 'a chain of feeds may not begin and end with the same array');

    @data = &lt;1 2 4 5 7 8&gt;;
    @data &lt;== grep {$_ % 2} &lt;== eager @data;
    # rakudo 2 todo 'feeds + eager'
    is(~@data, ~@odds, '@arr &lt;== grep &lt;== eager @arr works');

    @data = &lt;1 2 4 5 7 8&gt;;
    @data &lt;== eager grep {$_ % 2} &lt;== @data;
    is(~@data, ~@odds, '@arr &lt;== eager grep &lt;== @arr works');
}

</code></pre>

<pre><code class="language-perl6">use v6;

use Test;

# L&lt;S06/&quot;Feed operators&quot;&gt;
# L&lt;S03/&quot;Feed operators&quot;&gt;

=begin pod

Tests for the feed operators

    ==&gt; and &lt;==

=end pod

plan 24;

{
    my @a = (1, 2);
    my (@b, @c);

    @a ==&gt; @b;
    @c &lt;== @a;

    is(~@b, ~@a, &quot;ltr feed as simple assignment&quot;);
    is(~@c, ~@a, &quot;rtl feed as simple assignment&quot;);
}

{
    my @a = (1 .. 5);
    my @e = (2, 4);

    my (@b, @c);
    @a ==&gt; grep { ($_ % 2) == 0 } ==&gt; @b;
    @c &lt;== grep { ($_ % 2) == 0 } &lt;== @a;
    my @f = do {@a ==&gt; grep {($_ % 2) == 0}};
    my @g = (@a ==&gt; grep {($_ % 2) == 0});

    is(~@b, ~@e, &quot;array ==&gt; grep ==&gt; result&quot;);
    is(~@c, ~@e, &quot;result &lt;== grep &lt;== array&quot;);
    is(~@f, ~@e, 'result = do {array ==&gt; grep}');
    is(~@g, ~@e, 'result = (array ==&gt; grep)');
}

{
    my ($got_x, $got_y, @got_z);
    sub foo ($x, $y?, *@z) {
        $got_x = $x;
        $got_y = $y;
        @got_z = @z;
    }

    my @a = (1 .. 5);

    @a ==&gt; foo &quot;x&quot;;

    is($got_x, &quot;x&quot;, &quot;x was passed as explicit param&quot;);
    #?rakudo 2 todo 'feeds + signatures'
    ok(!defined($got_y), &quot;optional param y was not bound to fed list&quot;);
    is(~@got_z, ~@a, '...slurpy array *@z got it');
}

{
    my @data = &lt;1 2 4 5 7 8&gt;;
    my @odds = &lt;1 5 7&gt;;

    eval_dies_ok('@data &lt;== grep {$_ % 2} &lt;== @data', 'a chain of feeds may not begin and end with the same array');

    @data = &lt;1 2 4 5 7 8&gt;;
    @data &lt;== grep {$_ % 2} &lt;== eager @data;
    #?rakudo 2 todo 'feeds + eager'
    is(~@data, ~@odds, '@arr &lt;== grep &lt;== eager @arr works');

    @data = &lt;1 2 4 5 7 8&gt;;
    @data &lt;== eager grep {$_ % 2} &lt;== @data;
    is(~@data, ~@odds, '@arr &lt;== eager grep &lt;== @arr works');
}

# checking the contents of a feed: installing a tap
{
    my @data = &lt;0 1 2 3 4 5 6 7 8 9&gt;;
    my @tap;

    @data &lt;== map {$_ + 1} &lt;== @tap &lt;== grep {$_ % 2} &lt;== eager @data;
    is(@tap, &lt;1 3 5 7 9&gt;, '@tap contained what was expected at the time');
    #?rakudo todo 'feeds + eager'
    is(@data, &lt;2 4 6 8 10&gt;, 'final result was unaffected by the tap variable');
}

# &lt;&lt;== and ==&gt;&gt; pretending to be unshift and push, respectively
# rakudo skip 'double-ended feeds'
{
    my @odds = &lt;1 3 5 7 9&gt;;
    my @even = &lt;0 2 4 6 8&gt;;

    my @numbers = do {@odds ==&gt;&gt; @even};
    is(~@numbers, ~(@even, @odds), 'basic ==&gt;&gt; test');

    @numbers = do {@odds &lt;&lt;== @even};
    is(~@numbers, ~(@odds, @even), 'basic &lt;&lt;== test');
}

# feeding to whatever using ==&gt; and ==&gt;&gt;

# rakudo skip 'double-ended feeds'
{
    my @data = 'a' .. 'e';

    @data ==&gt; *;
    is(@(*), @data, 'basic feed to whatever');

    &lt;a b c d&gt; ==&gt;  *;
    0 .. 3    ==&gt;&gt; *;
    is(@(*), &lt;a b c d 0 1 2 3&gt;, 'two feeds to whatever as array');
}

# feed and Inf
# nieza skip &quot;unhandled exception
{
  lives_ok { my @a &lt;== 0..Inf }
}

# nieza skip &quot;Unhandled exception&quot;
{
  my $call-count = 0;
  my @a &lt;== gather for 1..10 -&gt; $i { $call-count++; take $i };
  @a[0];
  #?rakudo todo &quot;isn't lazy&quot;
  is $call-count, 1;
}

# no need for temp variables in feeds: $(*), @(*), %(*)
# rakudo skip '* feeds'
# DOES 4
{
    my @data = 'a' .. 'z';
    my @out  = &lt;a e i o u y&gt;;

    @data ==&gt; grep {/&lt;[aeiouy]&gt;/} ==&gt; is($(*), $(@out), 'basic test for $(*)');
    @data ==&gt; grep {/&lt;[aeiouy]&gt;/} ==&gt; is(@(*), @(@out), 'basic test for @(*)');
    @data ==&gt; grep {/&lt;[aeiouy]&gt;/} ==&gt; is(%(*), %(@out), 'basic test for %(*)');

    # XXX: currently the same as the @(*) test above. Needs to be improved
    @data ==&gt; grep {/&lt;[aeiouy]&gt;/} ==&gt; is(@(*).slice, @(@out).slice, 'basic test for @(*).slice');
}


done;

# vim: ft=perl6

</code></pre>

<pre><code class="language-perl6">use v6;
use Test;
{
    my ($got_x, $got_y, @got_z);
    sub foo ($x, $y?, *@z) {
        $got_x = $x;
        $got_y = $y;
        @got_z = @z;
    }

    my @a = (1 .. 5);

    @a ==&gt; foo &quot;x&quot;;

    is($got_x, &quot;x&quot;, &quot;x was passed as explicit param&quot;);
    # rakudo 2 todo 'feeds + signatures'
    ok(!defined($got_y), &quot;optional param y was not bound to fed list&quot;);
    is(~@got_z, ~@a, '...slurpy array *@z got it');
}


</code></pre>

<pre><code class="language-perl6">﻿my @a =slurp('Hamlet.txt').comb(/\w+/);
my @result = (@a ==&gt; sort *.chars ==&gt;  reverse @() ==&gt; join &quot;\n&quot;);
say  @result; # @() 不使用临时数组存储中间变量
</code></pre>

<h2 id="hyper-operator">Hyper Operator</h2>

<hr />

<pre><code class="language-perl6">use v6;

sub add($x) {
    sleep 3;
    $x ** 2 + 1;
}
# hyper  运算符现在还未实现并行， 该程序 sleep 了 9 秒
my @array = 1, 3, 5;
.say for @array».&amp;add;
</code></pre>

<p>超运算符能运用在自定义的子例程上面：</p>

<p>为什么会使用 <code>.&amp;</code> 语法呢？
因为 $obj.method 确实是一个方法调用， 而 <code>$obj.$coderef</code> 不是
add 函数的名字是 <code>&amp;add</code>,  就像 foo 标量的名字是 <code>$foo</code>, 数组 foo 的名字叫做 @foo 一样
在 Perl 6 中， 如果你提到一个子例程却不带 &amp; 符号， 那就是调用了它。在 add 前面加上 &amp;  符号才能引用一个函数。</p>

<pre><code class="language-perl6">&gt; sub add($x) {$x * 2 + 1}
sub add (Any $x) { #`(Sub|140286460109400) ... }
&gt; 2.add
Method 'add' not found for invocant of class 'Int'
&gt; 2.&amp;add
5
&gt; add(2)
5
&gt; my $function = sub add($x) {$x * 2 + 1}
sub add (Any $x) { #`(Sub|140286460109552) ... }
&gt; 2.$function
5
&gt; $function(2)
5
&gt; my @a = 1, 2, 3
1 2 3
&gt; @a&gt;&gt;.$function
3 5 7
&gt; say $function.WHAT
(Sub)


$obj.$function 等价于 $function($obj)
</code></pre>

<h2 id="meta-operators">Meta Operators</h2>

<hr />

<pre><code class="language-perl6">﻿use v6;

my @lines = slurp('3col.txt');
for @lines -&gt; $line {
   my @b = $line.comb(/\d+/);
   say &quot;@b[]&quot;;
   say &quot;-&quot; x 45;
}

# 没有打印出满意的结果, 因为 slurp 是把所有文本作为一个字符串吸入的.

#`(
my $fh = open('3col.txt');
my $num;
for $fh.lines -&gt; $line {
    $num += $line.words.[2];
}
say $num;
)

my $fh = open('3col.txt');
# say [+] ($fh.lines&gt;&gt;.words).[2];
my @l = $fh.lines&gt;&gt;.comb(/\d+/);
say @l.elems;
</code></pre>

<pre><code class="language-perl6">﻿use v6;

# 该源文件必须保存为 UTF8 格式 才不会报 UTF-8 错误, 即使是中文注释

sub infix:&lt;中&gt;(@array, $ins) {
    @array.splice(+@array / 2, 0, $ins);
    return @array;
}

my @a = 1,2,4,5;
say @a 中 3;

</code></pre>

<pre><code class="language-perl6"># Loop over multiple arrays (or lists or tuples or whatever they're called in your language) and print the ith element of each. Use your language's &quot;for each&quot; loop if it has one, otherwise iterate through the collection in order with some other loop.

# For this example, loop over the arrays (a,b,c), (A,B,C) and (1,2,3) to produce the output
# aA1
# bB2
# cC3

for &lt;a b c&gt; Z &lt;A B C&gt; Z 1, 2, 3 -&gt; $x, $y, $z {
   say $x, $y, $z;
}

# The Z operator stops emitting items as soon as the shortest input list is exhausted. However, short lists are easily extended by replicating all or part of the list, or by appending any kind of lazy list generator to supply default values as necessary.

# Note that we can also factor out the concatenation by making the Z metaoperator apply the ~ concatenation operator across each triple:
for &lt;a b c&gt; Z~ &lt;A B C&gt; Z~ 1, 2, 3 -&gt; $line {
   say $line;
}

# We could also use the zip-to-string with the reduction metaoperator:
.say for [Z~] [&lt;a b c&gt;], [&lt;A B C&gt;], [1,2,3]

</code></pre>

<pre><code class="language-perl6">﻿use v6;

class PowerBy2 {
    has $.number;

    method power_by2() {
        return  $.number ** 2;
    }
}

my  $test = PowerBy2.new(number=&gt;10);
say $test.power_by2;

my @a = &lt;1 2 3 4&gt;;
my @b = @a&gt;&gt;.power_by2;
say @b;
</code></pre>

<pre><code class="language-perl6"> use v6;

 my @scores = 'Ana' =&gt; 8, 'Dave' =&gt; 6, 'Charlie' =&gt; 4, 'Beth' =&gt; 4;

 my $screen-width = 30;

 my $label-area-width = 1 + [max] @scores».key».chars;
 my $max-score = [max] @scores».value;
 my $unit = ($screen-width - $label-area-width) / $max-score;
 my $format = '%- ' ~ $label-area-width ~ &quot;s%s\n&quot;;

 for @scores {
 printf $format, .key, 'X' x ($unit * .value);
 }
</code></pre>

<pre><code class="language-perl6">use v6;
use List::Utils;

my @array = &lt;1 3 4 6 7&gt;;
my @b = sliding-window(@array,2);
.say for @b;
say '-' x 15;
my @c = combinations(@array);
.say for @c;
say '-' x 15;

my @d = combinations(@array,3);
.say for @d;

say '-' x 15;
my @e = combinations(@array,4);
.say for @e;

say '-' x 15;
my @take = take-while((1...*), * &lt;= 10);
.say for @take;

say '-' x 15;
my @takerange = take-while((1...*), * + 4 &lt; 10);
.say for @takerange;

say '-' x 15;
my @aa = uniq-by(&lt;A B C a b c d D e&gt;, *.uc);
my @bb = uniq-by((1..* Z -2..*), *.abs)[^10];
my @cc = uniq-by((1..* Z -2..*), *+1)[^10];
.say for @aa;
say '-' x 15;
.say for @bb;
say '-' x 15;
.say for @cc;
</code></pre>

<pre><code class="language-perl6">use Test;
use List::Utils;

is take-while((1...*), * &lt;= 10), ~(1...10), &quot;take-while works on a basic infinite loop&quot;;
is take-while((1...*), * &lt;= -1), &quot;&quot;, &quot;take-while works if condition is initially false&quot;;

done;

</code></pre>

<pre><code class="language-perl6">my @a = 0..9;
my @b = 'a'..'z';
my @guess = @a X~ @b;
say @guess;

</code></pre>

<pre><code class="language-perl6">﻿use v6;
my @a=1,2,3;
my @b=4,5,6;
my @c=7,8,9;
for zip(@a; @b; @c) -&gt; $a, $b, $c {say $a,$b,$c;}

# 多行注释
my $things = #`( i wonder how many of these
 I will need, hm maybe 3, or 4, better 5 ) 5; # same as $things = 5;
say $things;

say Q/hello/;
say Q{world};
say Q|1234|;
say Q,comma,;
say Q[maohao];
say Q*askiles*;
say Q:a/@a[0]/;  # 1
say @a[1];


</code></pre>

<pre><code class="language-perl6">use v6;

my @a = &lt;王 孙 刘&gt;;
my @b = &lt;伟 亦 百&gt;;
my @c = &lt;霆 哲 爽&gt;;
for zip(@a;@b;@c) -&gt; $a,$b,$c {
    say &quot;$a$b$c&quot;;
}

</code></pre>

<pre><code class="language-perl6">use v6;

my @a=&lt;aa bb cc dd ee ff &gt;;
for @a -&gt; $x,$y,$z {
    say $x,$y,$z;
}
say $*PROGRAM_NAME;
say $*CWD;
# aabbcc
# ddeeff
# three.p6
</code></pre>

<pre><code class="language-perl6">One place infinite lazy lists do not work are the hyper meta operators
The idea is that conceptually they work on the entire list at once
Indeed, they are allowed to work on its elements in any order, and in parallel
(In practice, none of the Perl 6 compilers handle parallel processing yet)
Our first example of a meta operator: an operator built from a simpler operator
@a »+« @b produces an array which is the sum of the other two arrays
@a »%%» 2 produces an array of Bools telling which elements of @a are divisible by 2
~«@a is effectively the same as @a».Str -- it returns an array of strings

当无限惰性列表遇到 hyper 元操作符时, 就不起作用了。从概念上来讲, 它们实际上立即作用于整个列表, 它们被允许以任意顺序作用它们的元素
还能并行。（事实上, 目前没有一个 Perl 6 编译器能处理并行）
我们第一个关于元操作符的例子：一个构建自简单操作符的操作符 `@a »+« @b` 生成了一个数组, 这个数组是另外两个数组对应元素的和。
@a »%%» 2 生成一个布尔值的数组, @a 中的哪个元素能被 2 整除。
~«@a 和 @a».Str 一样 -- 返回一个字符串的数组。

如果你需要对两个无限惰性列表的元素进行求和, 你可以使用 `zip` 元操作符, 使用 `@a Z+ @b` 代替 `@a »+« @b`。
`Z+` 对列表进行惰性求值并按需返回它们的值。
等价于  `(@a Z @b).map(* + *)`。
&lt;a b c&gt; Z~ &lt;1 2 3&gt; 返回 a1 b2 c3
同样地 , 交叉操作符 X 有一个等价的 元操作符
&lt;a b c&gt; X~ &lt;1 2 3&gt; 返回 a1 a2 a3 b1 b2 b3 c1 c2 c3

If you do need to sum the elements of two infinite lazy lists, you can use the zip meta operator
Instead of @a »+« @b, you can do @a Z+ @b
Z+ evaluates the lists lazily and returns its values lazily
It's effectively doing (@a Z @b).map(* + *)
&lt;a b c&gt; Z~ &lt;1 2 3&gt; returns a1 b2 c3
Likewise, the cross operator X has a meta operator equivalent
&lt;a b c&gt; X~ &lt;1 2 3&gt; returns a1 a2 a3 b1 b2 b3 c1 c2 c3

另外一个作用于数组/列表的元操作符是 reduce:
`[+] @a` 对 @a 的所有元素进行求和并返回和, 这在功能上等价于 `@a[0] + @a[1] + ... + @a[*-1]`
任何中缀操作符都能用在 `+` 那个位置上, 显然这对无限惰性列表没有作用。
但是有另外一种形式的 reduce 元操作符, 它能返回一个惰性列表。
`[\*] 1..*` 返回一个惰性列表  `1, 1*2, 1*2*3, 1*2*3*4 ...`
那就是说, it returns each internal step of the evaluation of [*]

Another meta operator which works on arrays/lists is reduce:
[+] @a sums all of the elements of @a and returns the sum
It's functionally equivalent to @a[0] + @a[1] + ... + @a[*-1]
Any infix operator can be used in place of + there
Obviously this will not work for infinite lazy lists
But there is another form of the reduce meta operator which returns a lazy list
[\*] 1...* returns the lazy list 1, 1*2, 1*2*3, 1*2*3*4 ...
That is to say, it returns each internal step of the evaluation of [*]

其它元操作符：
赋值： 传统 op= (eg +=)
取反： 中缀关系操作符能用作 !op, 即 ` $a !eq $b ` 等价于 `!($a eq $b)`。
反转： Rop 反转了 op 的参数, 所以 $a R- $b 和 $b - $a 相同。

Other meta operators:
Assignment: The traditional op= (eg +=)
Negation: Infix relational operators can be used as !op
That is, $a !eq $b is equivalent to !($a eq $b)
Reversing: Rop reverses the arguments to op
So $a R- $b is the same as $b - $a

&quot;自然地&quot;, 元操作符能嵌套起来
&quot;Naturally,&quot; meta operators can be nested
&lt;a b c&gt; X~ &lt;1 2 3&gt;   is a1 a2 a3 b1 b2 b3 c1 c2 c3
&lt;a b c&gt; RX~ &lt;1 2 3&gt;  is 1a 1b 1c 2a 2b 2c 3a 3b 3c
&lt;a b c&gt; RXR~ &lt;1 2 3&gt; is a1 b1 c1 a2 b2 c2 a3 b3 c3
That's one of the few useful applications of this I know of :)

New operators can be defined just like any other sub
multi sub infix:&lt;+&gt;(MyInt $a, MyInt $b) overloads addition for MyInt
sub postfix:&lt;!&gt;(Int $a) { [*] 1..$a; } creates a factorial operator:
5! yields 120, just as you would expect
sub prefix:&lt;$$$&gt;($a) { &quot;$a billion dollars&quot; }
$$$10 yields the string 10 billion dollars
In theory, these new operators can be used with meta operators too

You may have the impression that Perl 6 is operator crazy
If so, you are right
Part of the Perl 6 philosophy is to have a very rich set of operators
It's up to programmers not to abuse this
eg As an infix, + (by itself) should always refer to mathematical addition
But that's a convention, not a hard technical rule of the language

prefix:&lt;+&gt; is just sugar for calling an object's Numeric conversion method
Operators which start with + are numeric operators
For instance, +&amp; converts both its arguments to Int and does bitwise AND on them
prefix:&lt;?&gt; is sugar for .Bool, and ?| converts its arguments to Bool and ORs them
prefix:&lt;~&gt; is sugar for .Str, conversion to a string



</code></pre>

<h2 id="syntax-flexibility">Syntax flexibility</h2>

<hr />

<h2 id="concurrency">Concurrency</h2>

<hr />

<h2 id="phasers">Phasers</h2>

<hr />

<h2 id="misc">Misc</h2>

<hr />

<h2 id="meta-object-programming">Meta-Object Programming</h2>

<hr />

<h2 id="lexing-and-parsing">Lexing and Parsing</h2>

<hr />

<h2 id="roles">Roles</h2>

<hr />

<p>所以到底什么是 role 呢？ 零个或多个方法和属性的集合。</p>

<p>role 不像class，它不能被实例化（如果你尝试了，会生成一个 class）。Perl 6 中 Classes 是可变的，而 roles 是不可变的。</p>

<p>声明 Roles 就像声明 Class 一样：</p>

<pre><code class="language-perl6">role DebugLog {
    has @.log_lines;
}

has $.log_size is rw = 100;

method log_message($message) {
@!log_lines.shift if
    @!log_lines.elems &gt;= $!log_size;
    @!log_lines.push($message);
}
</code></pre>

<p>使用 <code>does</code> trait 将 role 组合到 Class 中：</p>

<pre><code class="language-perl6">class WebCrawler does DebugLog {
    ...
}
</code></pre>

<p>这会把方法和属性添加到 class WebCrawler 里面去。结果就像它们起初被写到 class 中一样。</p>

<pre><code class="language-perl6">role Bull-Like {
    has Bool $.castrated = False;
    method steer {
        # Turn your bull into a steer
        $!castrated = True;
        return self;
    }
}
role Steerable {
    has Real $.direction;
    method steer (Real $d = 0) {
        $!direction += $d;
    }
}
class Taurus does Bull-Like does Steerable {
    method steer ($!direction?) {
        self.Steerable::steer($!direction?);
    }
 }

</code></pre>

<pre><code class="language-perl6">role Hammering {
    method hammer($stuff) {
        say &quot;You hammer on $stuff. BAM BAM BAM!&quot;;
    }
}

class Hammer does Hammering {}
class Gavel  does Hammering {}
class Mallet does Hammering {}

my $hammer = Hammer.new;    # create a new hammer object
say $hammer ~~ Hammer;      # &quot;Bool::True&quot; -- yes, this we know
say $hammer ~~ Hammering;   # &quot;Bool::True&quot; -- ooh!

my $unkown_object = Gavel.new;
if $unkown_object ~~ Hammering {
    $unkown_object.hammer(&quot;that nail over there&quot;);     # will always work
}

</code></pre>

<pre><code class="language-perl6">role Serializable {
    method serialize() {
        self.perl; # very primitive serialization
    }
    method deserialize($buf) {
        EVAL $buf; # reverse operation to .perl
    }
}

class Point does Serializable {
    has $.x;
    has $.y;
}
my $p = Point.new(:x(1), :y(2));
my $serialized = $p.serialize;      # method provided by the role
say $serialized;
my $clone-of-p = Point.deserialize($serialized);
say $clone-of-p.x;      # 1

</code></pre>

<pre><code class="language-perl6">﻿role Observable {
    has @!observers;

    method subscribe($observer) {
        push @!observers, $observer;
        $observer
    }

    method unsubscribe($observer) {
        @!observers .= grep({ $^o !=== $observer });
    }

    method publish($obj) {
        @!observers&gt;&gt;.handle($obj)
    }
}

class ReadLineSource does Observable {
    has $.fh;
    method enterloop() {
        loop {
            self.publish($.fh.get());
        }
    }
}

multi grep($matcher, Observable $ob) {
    my class GrepSubscriber does Observable {
        has $.matcher;
        method handle($obj) {
            if $obj ~~ $.matcher {
                self.publish($obj);
            }
        }
    }
    $ob.subscribe(GrepSubscriber.new(:$matcher))
}

my $src = ReadLineSource.new(fh =&gt; $*IN);
$src
    ==&gt; grep(/^\d+$/)
    ==&gt; into my $nums;

$nums
    ==&gt; grep(*.Int.is-prime)
    ==&gt; call(-&gt; $p { say &quot;That's prime!&quot; });

$nums
    ==&gt; map(-&gt; $n {
            state $total += $n;
            $total &gt;= 100 ?? 'More than 100' !! ()
        })
    ==&gt; first()
    ==&gt; call(-&gt; $msg { say $msg });
</code></pre>

<pre><code class="language-perl6">role Point {
    has $.x;
    has $.y;
    method abs { sqrt($.x * $.x + $.y * $.y) }
}
say Point.new(x =&gt; 6, y =&gt; 8).abs;

</code></pre>

<pre><code class="language-perl6">role Sleeping {
    method lie {
        say &quot;Reclining horizontally...&quot;;
    }
}

role Lying {
    method lie {
        say &quot;Telling an untruth...&quot;;
    }
}

# 如果解决方法同名的冲突呢？
# 在 class 中定义一个同名的方法即可
class SleepingLiar does Sleeping does Lying {
    method lie {
        say &quot;Lying in my sleep....&quot;;
    }
}

my $sleep = SleepingLiar.new;
$sleep.lie; # Lying in my sleep....

# 调用其中之一的 roles 的 lie 方法
class SleepingSheep does Sleeping does Lying {
    method lie {
        self.Sleeping::lie;
    }
}

my $sleepSheep = SleepingSheep.new;
$sleepSheep.lie; # Reclining horizontally...

</code></pre>

<pre><code class="language-perl6">role Paintable {
    has $.colour is rw;
    method paint { ... }
}
class Shape {
    method area { ... }
}

class Rectangle is Shape does Paintable {
    has $.width;
    has $.height;
    method area {
        $!width * $!height;
    }
    method paint() {
        for 1..$.height {
            say 'x' x $.width;
        }
    }
}

Rectangle.new(width =&gt; 8, height =&gt; 3).paint;
# 这打印下面 3 行
xxxxxxxx
xxxxxxxx
xxxxxxxx

</code></pre>

<pre><code class="language-perl6">role Serializable {
    method serialize() {
        self.perl; # very primitive serialization
    }
    method deserialization-code($buf) {
        EVAL $buf; # reverse operation to .perl
    }
}

class Point does Serializable {
    has $.x;
    has $.y;
}
my $p = Point.new(:x(1), :y(2));
my $serialized = $p.serialize;      # method provided by the role
my $clone-of-p = Point.deserialization-code($serialized);
say $clone-of-p.x;      # 1

</code></pre>

<pre><code class="language-perl6">role Sleeping {
    method lie {
        say &quot;Reclining horizontally...&quot;;
    }
}

role Lying {
    method lie {
        say &quot;Telling an untruth...&quot;;
    }
}

class SleepingLiar does Sleeping does Lying {}    # CONFLICT!

# Method 'lie' must be resolved by class SleepingLiar because it exists in multiple roles (Lying, Sleeping)

</code></pre>

<h2 id="rosettacode">rosettacode</h2>

<hr />

<pre><code class="language-perl6"># 问题： 你有 100 扇关着的门排成一排， 然后你穿过这些门 100 次。第一次穿过的时候，穿越每一扇门， 如果门是开着的就关闭它， 如果门是关着的就打开它。第二次穿越的时候，每两扇门穿越一下，（第 2、4、6扇门）；第三次穿越的时候， 每 3 扇门（第 3、6、9），等等， 直到你穿过第 100 扇门为止。
# 问： 最后一次穿过门之后， 每扇门的状态是开是关？
# 提示： 剩下开着的门就是那些能开方的整数the only doors that remain open are whose numbers are perfect squares of integers
#`(
my @doors = False xx 101;

($_ = !$_ for @doors[0, * + $_ ...^ * &gt; 100]) for 1..100;

say &quot;Door $_ is &quot;, &lt;closed open&gt;[ @doors[$_] ] for 1..100;
)

say &quot;Door $_ is open&quot; for map {$^n ** 2}, 1..10;

say &quot;Door $_ is open&quot; for 1..10 X** 2;

say &quot;Door $_ is &quot;, &lt;closed open&gt;[.sqrt == .sqrt.floor] for 1..100;

# « U+00AB  , » U+00BB  Vim =&gt; Ctrl+V =&gt; u =&gt; 00AB


</code></pre>

<pre><code class="language-perl6"># Works with: Rakudo Star version 2010.08
for 10 ... 0 {
    .say;
}

</code></pre>

<pre><code class="language-perl6"># Works with: Rakudo Star version 2010.08
loop {
    say 'SPAM';
}

# In addition, there are various ways of writing lazy, infinite lists in Perl 6:
print &quot;SPAM\n&quot; xx *;      # repetition operator
print &quot;SPAM\n&quot;, ~* ... *; # sequence operator
map {say &quot;SPAM&quot;}, ^Inf;   # upto operator

</code></pre>

<pre><code class="language-perl6"># A list comprehension is a special syntax in some programming languages to describe lists. It is similar to the way mathematicians describe sets, with a set comprehension, hence the name.

# Some attributes of a list comprehension are that:
# 1. They should be distinct from (nested) for loops within the syntax of the language.
# 2. They should return either a list or an iterator (something that returns successive members of a collection, in order).
# 3. The syntax has parts corresponding to that of set-builder notation.

# Write a list comprehension that builds the list of all Pythagorean triples with elements between 1 and n. If the language has multiple ways for expressing such a construct (for example, direct list comprehensions and generators), write one example for each.

use v6;

my $n = 20;
my @list := gather for 1..$n -&gt; $x {
         for $x..$n -&gt; $y {
           for $y..$n -&gt; $z {
             take $x,$y,$z if $x*$x + $y*$y == $z*$z;
           }
         }
       }
.say for  @list;

# Note that gather/take is the primitive in Perl 6 corresponding to generators or coroutines in other languages. It is not, however, tied to function call syntax in Perl 6. We can get away with that because lists are lazy, and the demand for more of the list is implicit; it does not need to be driven by function calls.
</code></pre>

<pre><code class="language-perl6"># Loop over multiple arrays (or lists or tuples or whatever they're called in your language) and print the ith element of each. Use your language's &quot;for each&quot; loop if it has one, otherwise iterate through the collection in order with some other loop.

# For this example, loop over the arrays (a,b,c), (A,B,C) and (1,2,3) to produce the output
# aA1
# bB2
# cC3

for &lt;a b c&gt; Z &lt;A B C&gt; Z 1, 2, 3 -&gt; $x, $y, $z {
   say $x, $y, $z;
}

# The Z operator stops emitting items as soon as the shortest input list is exhausted. However, short lists are easily extended by replicating all or part of the list, or by appending any kind of lazy list generator to supply default values as necessary.

# Note that we can also factor out the concatenation by making the Z metaoperator apply the ~ concatenation operator across each triple:
for &lt;a b c&gt; Z~ &lt;A B C&gt; Z~ 1, 2, 3 -&gt; $line {
   say $line;
}

# We could also use the zip-to-string with the reduction metaoperator:
.say for [Z~] [&lt;a b c&gt;], [&lt;A B C&gt;], [1,2,3]

</code></pre>

<pre><code class="language-perl6"># 给定一组排好序的数， 如果数字是连续的，就用 - 符号连接 头和尾
# 例如 -6, -3, -2, -1, 0, 1, 3, 4, 5, 7, 8, 9, 10, 11, 14, 15, 17, 18, 19, 20
# 处理后变为： -6,-3-1,3-5,7-11,14,15,17-20
# Task:编写一个函数， 将范围连接起来
#  0,  1,  2,  4,  6,  7,  8, 11, 12, 14,
#  15, 16, 17, 18, 19, 20, 21, 22, 23, 24,
#  25, 27, 28, 29, 30, 31, 32, 33, 35, 36,
#  37, 38, 39

sub range-extraction (*@ints) {
    my $prev = NaN;
    my @ranges;

    for @ints -&gt; $int {
        if $int == $prev + 1 {
            @ranges[*-1].push: $int;
        }
        else {
            @ranges.push: [$int];
        }
        $prev = $int;
    }
    join ',', @ranges.map: -&gt; @r { @r &gt; 2 ?? &quot;@r[0]-@r[*-1]&quot; !! @r }
}

say range-extraction
    -6, -3, -2, -1, 0, 1, 3, 4, 5, 7, 8, 9, 10, 11, 14, 15, 17, 18, 19, 20;

say range-extraction
    0,  1,  2,  4,  6,  7,  8, 11, 12, 14,
    15, 16, 17, 18, 19, 20, 21, 22, 23, 24,
    25, 27, 28, 29, 30, 31, 32, 33, 35, 36,
    37, 38, 39;

</code></pre>

<pre><code class="language-perl6"># The sleep function argument is in units of seconds, but these may be fractional (to the limits of your system's clock).
my $sec = prompt(&quot;Sleep for how many microfortnights? &quot;) * 1.2096;
say &quot;Sleeping...&quot;;
sleep $sec;
say &quot;Awake!&quot;;

</code></pre>

<pre><code class="language-perl6"># Sort an array of composite structures by a key. For example, if you define a composite structure that presents a name-value pair (in pseudocode):
# Define structure pair such that:
   # name as a string
   # value as a string


# and an array of such pairs:
# x: array of pairs


# then define a sort routine that sorts the array x by the key name.

# This task can always be accomplished with Sorting Using a Custom Comparator. If your language is not listed here, please see the other article.

# Works with: Rakudo Star version 2010.08

my class Employee {
   has Str $.name;
   has Rat $.wage;
}

my $boss     = Employee.new( name =&gt; &quot;Frank Myers&quot;     , wage =&gt; 6755.85 );
my $driver   = Employee.new( name =&gt; &quot;Aaron Fast&quot;      , wage =&gt; 2530.40 );
my $worker   = Employee.new( name =&gt; &quot;John Dude&quot;       , wage =&gt; 2200.00 );
my $salesman = Employee.new( name =&gt; &quot;Frank Mileeater&quot; , wage =&gt; 4590.12 );

my @team = $boss, $driver, $worker, $salesman;

my @orderedByName = @team.sort( *.name )&gt;&gt;.name;
my @orderedByWage = @team.sort( *.wage )&gt;&gt;.name;

say &quot;Team ordered by name (ascending order):&quot;;
say @orderedByName.join('  ');
say &quot;Team ordered by wage (ascending order):&quot;;
say @orderedByWage.join('  ');

# this produces the following output:
# Team ordered by name (ascending order):
# Aaron Fast   Frank Mileeater   Frank Myers   John Dude
# Team ordered by wage (ascending order):
# John Dude   Aaron Fast   Frank Mileeater   Frank Myers


# Note that when the sort receives a unary function, it automatically generates an appropriate comparison function based on the type of the data.

</code></pre>

      </article>

      <ul class="pager blog-pager">
        
        <li class="previous">
          <a href="https://ohmysummer.github.io/post/Perl6%E4%B8%AD%E7%9A%84%E5%87%BD%E6%95%B0%E7%AD%BE%E5%90%8D/" data-toggle="tooltip" data-placement="top" title="Perl 6 中的函数签名">&larr; Previous Post</a>
        </li>
        
        
        <li class="next">
          <a href="https://ohmysummer.github.io/post/Meta-Operator/" data-toggle="tooltip" data-placement="top" title="Meta Operator">Next Post &rarr;</a>
        </li>
        
      </ul>

      

    </div>
  </div>
</div>

      
    </div>

    <footer>
  <div class="container">
    <div class="row">
      <div class="col-lg-8 col-lg-offset-2 col-md-10 col-md-offset-1">
        <ul class="list-inline text-center footer-links">
          
          
          
          
		      
          
		      
          
	    	  
           
           
          
          
          
          
          

    		  <li>
      			<a href="https://ohmysummer.github.ioindex.xml" title="RSS">
      			  <span class="fa-stack fa-lg">
        				<i class="fa fa-circle fa-stack-2x"></i>
        				<i class="fa fa-rss fa-stack-1x fa-inverse"></i>
      			  </span>
      			</a>
    		  </li>		

        </ul>
        <p class="credits copyright text-muted">
    	  
    	  &nbsp;&nbsp;
    	  © 2015 - 2017
    		  
    	  
    	  &nbsp;&bull;&nbsp;
    	  <a href="https://ohmysummer.github.io">Young For Perl 6</a>
    	  
  	    </p>
  	    
    	<p class="credits theme-by text-muted">
          <a href="http://gohugo.io">Hugo v0.18.1</a> powered &nbsp;&bull;&nbsp; Theme by <a href="https://github.com/halogenica/beautifulhugo">Beautiful Hugo</a>
          
    	</p>
      </div>
    </div>
  </div>
</footer>

<script src="https://code.jquery.com/jquery-1.12.4.min.js" integrity="sha256-ZosEbRLbNQzLpnKIkEdrPv7lOy9C27hHQ+Xp8a4MxAQ=" crossorigin="anonymous"></script>
<script src="https://maxcdn.bootstrapcdn.com/bootstrap/3.3.7/js/bootstrap.min.js" integrity="sha384-Tc5IQib027qvyjSMfHjOMaLkfuWVxZxUPnCJA7l2mCWNIpG9mGCD8wGNIcPD7Txa" crossorigin="anonymous"></script>
<script src="https://ohmysummer.github.io/js/main.js"></script>
<script src="https://ohmysummer.github.io/js/highlight.min.js"></script>
<script>
  hljs.initHighlightingOnLoad();
</script>




  </body>
</html>
